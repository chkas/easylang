txt_locale = "de"
txt_tutor = String.raw`+ Learn programming 1

+de Programmieren lernen 1

-

+ When programming, you tell the computer what to do with a sequence of special commands.

+de Beim Programmieren sagst du dem Computer mit einer Folge von speziellen Befehlen, was er machen soll.

##19
#
x = 20
glinewidth 8
gcolor 333
gline x + 9 96 x + 13 96
gcolor 900
gline x + 4 91 x + 18 91
gcolor 333
gcircle x + 5 85 3.5
gcircle x + 16 85 3.5

+ You can tell the computer to draw two thick round lines in different colors at certain positions and then two black circles - and there you have a car on the screen.

+ If this is done repeatedly with a short pause at slightly shifted positions, the car will drive. How to program this and other things you can learn here.

+de Man kann den Computer anweisen, zwei dicke runde Striche in verschiedenen Farben an bestimmten Positionen zu zeichnen und dann noch zwei schwarze Kreise  - und schon hat man ein Auto auf dem Bildschirm.

+de Wenn dies wiederholend mit einer kurzen Pause bei leicht verschobenen Positionen gemacht wird, f√§hrt das Auto. Wie man dies und andere Dinge programmiert, erf√§hrst du hier.

* Graphics

*de Grafik

+ First we look at the commands that can be used to create graphics on the screen.

+de Zuerst schauen wir uns die Befehle an, mit denen Grafiken auf dem Bildschirm erstellt werden k√∂nnen.

gcircle 10 30 1
gcircle 50 50 1

+ We draw a small circle (size 1) at the position (10 30), which is 10 from the left and 30 from the bottom, and one at the position (50 50) - this is in the middle of the drawing area.

+de Wir zeichnen einen kleinen Kreis (Gr√∂√üe 1) an der Position (10 30), das ist 10 von links und 30 von unten, und einen an der Position (50 50) - das ist in der Mitte der Zeichenfl√§che.

+ Then we connect these two points with a line.

+de Dann verbinden wir diese beiden Punkte mit einer Linie.

gcircle 10 30 1
gcircle 50 50 1
gline 10 30 50 50

##
gcolor 555
gtextsize 4
gtext 6 5 "0/0"
gtext 82 5 "100/0"
gtext 82 91.5 "100/100"
gtext 6 91.5 "0/100"
glinewidth 0.5
x = 10
while x <= 90
   gline x 10 x 90
   gline 10 x 90 x
   x += 8
.
gtext 12 28 "10/30"
gtext 44 52 "50/50"
gcolor 833
gcircle 18 34 1.2
gcircle 50 50 1.2
glinewidth 1
gline 18 34 50 50

+ The drawing area is 100 times 100 units. The origin is bottom left. The first value (the X coordinate) is the distance from the left edge, the second value (the Y coordinate) is the distance from the bottom edge.

+de Die Zeichenfl√§che betr√§gt 100 mal 100 Einheiten. Der Ursprung ist links unten. Der erste Wert (die X-Koordinate) ist der Abstand vom linken Rand, der zweite Wert (die Y-Koordinate) ist der Abstand vom unteren Rand.

-

+ *drawgrid* draws a grid. This makes it easier to determine the x and y positions for the graphics commands. In the finished program you can then omit this command.

+ ü§î Try to finish drawing the square!

+de *drawgrid* zeichnet ein Raster. Damit lassen sich die x- und y-Positionen f√ºr die Grafikbefehle leichter bestimmen. Im fertigen Programm kannst du diesen Befehl dann weglassen.

+de ü§î Versuche das Quadrat fertig zu zeichnen!

drawgrid
gline 10 20 30 20
gline 30 20 30 40

+ You can start the program with the *Run* button, with *Ctrl+R* or with *Shift+Enter*.

+de Das Programm kann mit dem *Run*-Button, mit *Strg+R* oder *Shift+Enter* gestartet werden.

-

+ With the command *grect* you can draw filled rectangles. The parameters specify the *x* and *y* position, the *width* and the *height*.

+ Most graphic commands begin with a *g* for *graphic*, so *grect* therefore stands for *graphic rectangle*.

+de Mit dem Befehl *grect* kann man gef√ºllte Rechtecke zeichnen. Die Parameter geben die *x*- und *y*-Position, die *Breite* und die *H√∂he* an.

+de Die meisten Grafikbefehle beginnen mit einem *g* f√ºr *Grafik*, *grect* steht also f√ºr *Grafisches Rechteck*.

gcolor 900
grect 10 20 50 30

##
gcolor 555
gtextsize 4
gtext 6 5 "0/0"
gtext 82 5 "100/0"
gtext 82 91.5 "100/100"
gtext 6 91.5 "0/100"
glinewidth 0.5
x = 10
while x <= 90
   gline x 10 x 90
   gline 10 x 90 x
   x += 8
.
gtext 12 22 "10/20"
gcolor 833
grect 18 26 40 24
gcolor 555
gcircle 18 26 1
gtext 35 22 "50"
gtext 59 37 "30"

+ *gcolor* sets the drawing color. *900*, for example, is red, *990* yellow and *444* grey.

+de *gcolor* setzt die Zeichenfarbe. *900* ist zum Beispiel rot, *990* gelb und *444* grau.

gcolor 990
gcircle 70 80 10
#
gcolor 444
grect 10 5 40 30

+ There are 1000 possible colors - from 000 to 999, mixed from the primary colours red, green and blue. The left digit specifies the red component, the middle digit the green component and the right digit the blue component. These are some possible colors.

+de Es gibt 1000 m√∂gliche Farben - und zwar von 000 bis 999. Die Farben werden dabei aus den Grundfarben Rot, Gr√ºn und Blau gemischt. Die linke Ziffer gibt den Rotanteil, die mittlere den Gr√ºnanteil und die rechte den Blauanteil an. Dies sind einige m√∂gliche Farben.

##84
col[] = [ 900 700 966 990 995 960 090 070 696 099 599 690 009 007 669 909 959 609 777 444 000 999 432 765 ]
gtextsize 7
for i to len col[]
   y = 100 - (i - 1) mod 6 * 14 - 14
   x = (i - 1) div 6 * 22
   gcolor col[i]
   grect x y 21 13
   s$ = col[i]
   if col[i] <= 9
      s$ = "00" & s$
   elif col[i] <= 99
      s$ = "0" & s$
   .
   gcolor 000
   if col[i] = 0 or col[i] = 432 : gcolor 888
   gtext x + 4 y + 4 s$
.

+ ü§î Draw a dark green square in the lower right area. Dark green is not present in the color palette shown. You can just take a green and lower the green component - the middle digit of the number.

+de ü§î Zeichne ein dunkelgr√ºnes Quadrat im unteren rechten Bereich. Dunkelgr√ºn ist in der abgebildeten Farbpalette nicht vorhanden. Du kannst einfach ein Gr√ºn nehmen und die Gr√ºn-Komponente - die mittlere Ziffer der Zahl - verringern.

-

+ The *tab* key - normally found to the left of the *Q* - helps you to complete commands. For example, to write *gcircle*, simply enter *g* or *gc* and then press the tab key repeatedly until *gcircle* appears on the screen.

+de Die *Tab*-Taste - normalerweise links neben dem *Q* zu finden - hilft dir dabei, Befehle zu vervollst√§ndigen. Um zum Beispiel *gcircle* zu schreiben, gib einfach *g* oder *gc* ein und dr√ºcke dann wiederholt die Tab-Taste, bis *gcircle* auf dem Bildschirm erscheint.

-

+ Drawing a house

+de Wir zeichnen ein Haus

# My house
gcolor 993
grect 20 0 60 45
# roof
gcolor 722
gpolygon [ 50 70 15 45 85 45 ]
# windows
gcolor 444
grect 30 10 10 10
grect 30 30 10 10
grect 60 30 10 10
# text
gtextsize 8
gtext 5 85 "MY HOUSE"

+ *gtext* writes a text at the specified position on the drawing area. *gpolygon [x1 y1 x2 y2 ..]* draws a filled polygon, for example a triangle, with the specified coordinates.

+de *gtext* schreibt einen Text an die angegebene Position auf der Zeichenfl√§che. *gpolygon [x1 y1 x2 y2 ..]* zeichnet ein gef√ºlltes Polygon, zum Beispiel ein Dreieck, mit den angegebenen Koordinaten.

+ The *#* character allows you to insert comments into the program.

+de Das *#*-Zeichen erlaubt es, Kommentare in das Programm einzuf√ºgen.

+ ü§î Draw the missing door. With the help of *drawgrid* it is easier to find the right positions.

+ ü§îü§î You can use a blue background as a sky and let a sun (*gcircle*) shine.

+de ü§î Zeichne die fehlende T√ºr. Mit Hilfe von *drawgrid* ist es einfacher, die richtigen Positionen zu finden.

+de ü§îü§î Du kannst einen blauen Hintergrund als Himmel verwenden und eine Sonne (*gcircle*) scheinen lassen.

+ In *Debug* mode, you can watch the computer execute its instructions one by one.

+de Im *Debug*-Modus kannst du dem Computer zuschauen, wie er nacheinander seine Anweisungen ausf√ºhrt.

-

+ The command *gcircle* draws a filled circle at the specified position with the specified radius.

+ *glinewidth* sets the line width. The lines are rounded at the ends.

+de Der Befehl *gcircle* zeichnet einen gef√ºllten Kreis an der angegebenen Position mit dem angegebenen Radius.

+de *glinewidth* stellt die Linienst√§rke ein. Die Linien sind an den Enden abgerundet.

gcircle 10 80 2
#
gcolor 990
gcircle 60 80 8
#
gcolor 060
glinewidth 2
gline 15 60 40 80
#
glinewidth 10
gcolor 900
gline 10 40 25 40
#
gcolor 333
gline 50 50 55 50

-

+ Now we can build a car using these parts.

+de Mit diesen Teilen k√∂nnen wir jetzt ein Auto bauen..

glinewidth 8
gcolor 333
gline 9 14 13 14
gcolor 900
gline 4 9 18 9
gcolor 333
gcircle 5 3 3.5

+ ü§î Something is missing ...

+de ü§î Da fehlt noch was ...

* Calculating, variable

*de Rechnen, Variable

+ With *print* (or *pr*) you can print numbers and text in the text output window.

+de Mit *print* (oder *pr*) kann man Zahlen und Texte im Textausgabefenster ausgeben.

print 7 * 8 + 2
pr 13 * 7

-

+ You can store a number in a variable and then work with this variable. The *=* here is an assignment command, not an equality expression.

+de Eine Zahl kann man in einer Variablen speichern und dann mit dieser Variablen arbeiten. Das *=* hier ist ein Zuweisungsbefehl, kein Gleichheitsausdruck.

l = 4
w = 5
a = l * w
print a

+ ü§î This small program calculated the area of a rectangle. Can you also calculate the circumference?

+de ü§î Dieses kleine Programm hat die Fl√§che eines Rechtecks berechnet. Kannst du auch den Umfang berechnen?

-

+ A variable can easily be incremented by a value: *a = a + 1*

+de Eine Variable kann man ganz einfach um einen Wert erh√∂hen: *a = a + 1*

a = 1
a = a + 1
print a

* Conditional statement

*de Bedingte Anweisung

+ With *if* you can instruct the computer to execute a block of commands only if a condition is met. The end of the block is indicated by *end*.

+de Mit *if* kannst du dem Computer sagen, dass er einen Block von Befehlen nur ausf√ºhren soll, wenn eine Bedingung erf√ºllt ist. Das Ende des Blocks wird durch *end* angezeigt.

a = 7
a = 9 * a
if a > 20
   print "WOW!"
   print "a is large."
end

##
gcolor 543
gtextsize 6
glinewidth 0.5
y = 75
proc xrect x y w h .
   gline x y x + w y
   gline x + w y x + w y + h
   gline x + w y + h x y + h
   gline x y + h x y
.
proc cod n$ v v$ .
   xrect 5 y + 2 75 20
   gtext 12 y + 11 n$
   if v >= 0
      gtext 52 y + 7 "a"
      xrect 59 y + 6 13 7.5
      gtext 62 y + 8 v
   else
      gtext 52 y + 7 v$
   .
   y -= 25
.
cod "a ‚Üê 7 " 7 ""
cod "a ‚Üê 9 * a " 63 ""
cod "a > 20 ?" -1 "YES"
cod "\"a is large\"" -1 ""

-

+ The block after *else* is executed if the condition is not fulfilled.

+de Der Block nach *else* wird ausgef√ºhrt, wenn die Bedingung nicht erf√ºllt ist.

a = random 10
b = random 10
print "What is " & a & " * " & b & " ?"
x = number input
print x
if a * b = x
   print "Thats wright üôÇ"
else
   print "Thats wrong üôÅ"
end

+ *random 10* returns a random number from 1 to 10. *number input* reads a number from the keyboard.

+de *random 10* liefert eine zuf√§llige Zahl von 1 bis 10. *number input* liest eine Zahl von der Tastatur ein.

* Loop

*de Schleife

+ *while* works similar to *if*, except that the conditional actions are executed again and again as long as the condition is fulfilled. This is called a *loop*.

+de *while* funktioniert √§hnlich wie *if*, nur dass die bedingten Aktionen immer wieder ausgef√ºhrt werden, solange die Bedingung erf√ºllt ist. Das nennt man eine *Schleife*.

i = 1
while i <= 4
   print i
   i = i + 1
end

+ ü§î Write a program that outputs the square of the numbers 1 to 10 (1, 4, 9 ...).

+ ü§îü§î Write a program that adds up the numbers from 1 to 10. (Result: 55)

+de ü§î Schreibe ein Programm, das das Quadrat der Zahlen 1 bis 10 ausgibt (1, 4, 9 ...).

+de ü§îü§î Schreibe ein Programm, das die Zahlen von 1 bis 10 zusammenz√§hlt. (Ergebnis: 55)

-

gline 10 10 90 10
gline 10 20 90 20
gline 10 30 90 30
gline 10 40 90 40
gline 10 50 90 50
gline 10 60 90 60
gline 10 70 90 70
gline 10 80 90 80
gline 10 90 90 90

+ ü§î Quite a lot of code. The lines of code appear repeatedly in a very similar form. You can certainly shorten the code using a loop.

+ ü§î Create a box pattern with additional vertical lines.

+de ü§î Ziemlich viel Code. Die Codezeilen erscheinen wiederholt in sehr √§hnlicher Form. Du kannst den Code sicherlich mit einer Schleife k√ºrzer machen.

+de ü§î Erzeuge mit zus√§tzlichen vertikalen Linien ein K√§stchenmuster.

+ ü§îü§î Can you create this pattern? With a loop you only need a few lines of code.

+de ü§îü§î Kannst du dieses Muster erzeugen? Mit einer Schleife brauchst du dazu nur ein paar Zeilen Code.

##
gcolor 555
glinewidth 0.7
while i <= 100
   gline i 0 100 i
   i += 10
.

-

+de Celsius in Fahrenheit umrechnen

+ Convert Celsius to Fahrenheit

c = 10
f = c * 1.8 + 32
print c & "¬∞C = " & f & "¬∞F"

+ ü§î Extend the program with a loop so that it outputs the values in Fahrenheit from 0¬∞C to 30¬∞C.

+de ü§î Erweitere das Programm mit einer Schleife so, dass es von 0¬∞C bis 30¬∞C die Werte in Fahrenheit ausgibt.

-

+ With a loop within a loop (nested loop) you can make even more interesting patterns.

+de Mit einer Schleife innerhalb einer Schleife (verschachtelte Schleife) kannst du noch interessantere Muster erzeugen.

gbackground 477
gclear
y = 5
while y < 100
   x = 5
   while x < 100
      gcircle x y 2.5
      sleep 0.02
      x += 10
   end
   y += 10
end

+ With each loop cycle of the outer loop, the inner loop is executed completely. *x += 10* is short for *x = x + 10* and means: increment *x* by 10.

+de Mit jedem Schleifenzyklus der √§u√üeren Schleife wird die innere Schleife vollst√§ndig ausgef√ºhrt. *x += 10* ist die Kurzform f√ºr *x = x + 10* und bedeutet: erh√∂he *x* um 10.

+ With *gbackground* you can set the color that *gclear* uses when clearing the drawing area. *sleep 0.02* puts a pause of 0.02 seconds.

+de  Mit *gbackground* kannst du die Farbe festlegen, die *gclear* beim L√∂schen der Zeichenfl√§che verwendet. *sleep 0.02* legt eine Pause von 0.02 Sekunden ein.

+ ü§î Change the program a little to make the pattern even more beautiful.

+de ü§î Ver√§ndere das Programm ein wenig, um das Muster noch sch√∂ner zu machen.

* Animation

*de Animation

+ A rolling ball - how does it work?

+ Draw the ball, wait briefly, clear the screen, move the position slightly, draw again, and so on. This creates the impression of movement.

+de Ein rollender Ball - wie funktioniert das?

+de Den Ball zeichnen, kurz warten, Bildschirm l√∂schen, die Position leicht ver√§ndern, und wieder zeichnen, und so fort. So entsteht der Eindruck einer Bewegung.

gcolor 900
x = -9
while x < 120
   # clear the drawing area
   gclear
   # draw the ball at position x
   gcircle x 10 10
   # wait half a second
   sleep 0.5
   # change drawing position
   x += 5
end

+ ü§î Try to make the animation smoother by increasing the x-position of the circle only by 1 or 0.5 each time and shortening the pause to the next frame.

+de ü§î Versuche, die Animation fl√ºssiger zu machen, indem du die x-Position des Kreises jedes Mal nur um 1 oder 0.5 erh√∂hst und die Pause zum n√§chsten Bild verk√ºrzt.

-

+ Now we draw a car instead of the ball.

+de Jetzt zeichnen wir statt dem Ball ein Auto.

subr drawcar
   glinewidth 8
   gcolor 333
   gline x + 9, 14, x + 13, 14
   gcolor 900
   gline x + 4, 9, x + 18, 9
   gcolor 333
   gcircle x + 5, 3, 3.5
   gcircle x + 16, 3, 3.5
end
#
x = -25
while x < 120
   gclear
   drawcar
   sleep 0.02
   x += 0.5
end

+ With *subr* you can combine actions that can be called from different places. This is called a *subroutine*.

+de Mit *subr* k√∂nnen Aktionen zusammengefasst werden, die von verschiedenen Stellen aus aufgerufen werden k√∂nnen. Das nennt man *Unterprogramm* oder *Subroutine*.

+ A comma can be used to separate the parameters of *gline* and *gcircle*. This is not necessary, but makes the code easier to read.

+de Mit einem Komma kann man die Parameter von *gline* und *gcircle* trennen. Dies ist nicht notwendig, macht den Code aber leichter lesbar.

+ ü§î Stop the car when it touches the right edge.

+ ü§îü§î Let the car move back again.

+de ü§î Halte das Auto an, wenn es die rechte Kante ber√ºhrt.

+de ü§îü§î Lass das Auto wieder zur√ºckfahren.

+ ü§îü§î Let the car move back and forth three times.

+de ü§îü§î Lass das Auto dreimal hin und her fahren.

+ ü§îü§î Draw the house from above as background.

+de ü§îü§î Zeichne das Haus von oben als Hintergrund.

-

+ With *proc* or *func* you can also create *procedures* or *functions* with parameters and local variables. This will be discussed in more detail later.

+de Mit *proc* oder *func* kann man auch *Prozeduren* oder *Funktionen* mit Parametern und lokalen Variablen erstellen. Darauf wird sp√§ter noch n√§her eingegangen.
`
