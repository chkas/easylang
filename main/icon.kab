sysconf topleft
#
# qlmanage -t -s 256 -o . icon.svg ; mv icon.svg.png icon.png
#
global width col$ .
func$ hdig d .
   d += 48
   if d >= 58
      d += 39
   .
   return strchar d
.
func$ hcol c .
   for i to 3
      d = c mod 10
      h = floor (d / 9 * 255)
      r$ = hdig (h mod 16) & r$
      r$ = hdig (h div 16) & r$
      c = c div 10
   .
   if r$ = "000000"
      r$ = "000"
   .
   return "#" & r$
.
proc xbackgr c .
   gbackground c
   gclear
   pr "<rect x=\"0\" y=\"0\" width=\"100%\" height=\"100%\" fill=\"" & hcol c & "\"/>"
.
proc xlinewidth w .
   width = w
   glinewidth w
.
proc xline x0 y0 x y .
   gline x0 y0 x y
   if col$ <> "#000"
      h$ = " fill=\"" & col$ & "\""
   .
   pr "<circle cx=\"" & x0 & "\" cy=\"" & y0 & "\" r=\"" & width / 2 & "\"" & h$ & "/>"
   pr "<circle cx=\"" & x & "\" cy=\"" & y & "\" r=\"" & width / 2 & "\"" & h$ & "/>"
   h$ = "<line x1=\"" & x0 & "\" y1=\"" & y0 & "\" x2=\"" & x & "\" y2=\"" & y & "\" "
   stroke$ = "stroke=\"" & col$ & "\" "
   pr h$ & stroke$ & "stroke-width=\"" & width & "\"/>"
   x0 = x
   y0 = y
.
proc xcolor c .
   gcolor c
   col$ = hcol c
.
proc xcircle x y r .
   gcircle x y r
   pr "<circle cx=\"" & x & "\" cy=\"" & y & "\" r=\"" & r & "\" fill=\"" & col$ & "\"/>"
.
proc draw .
   c = 789
   xbackgr c
   xcolor 000
   xlinewidth 6
   xline 24 22 76 22
   xline 76 22 76 64
   xline 76 64 24 64
   xline 24 64 24 22
   xlinewidth 10
   xline 20 72 80 72
   #
   xlinewidth 2
   y = 30
   for y = 30 step 5 to 55
      if y div 10 = 4
         xline 40 y 55 y
      else
         xline 30 y 45 y
      .
   .
   xcolor c
   xcircle 75 64 2
.
pr "<svg xmlns=\"http://www.w3.org/2000/svg\""
pr "width=\"512\" height=\"512\" viewBox=\"0 0 100 100\">"
draw
pr "</svg>"
