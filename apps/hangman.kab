# Hangman
#
if sysfunc "lang" = "de"
  alpha$[] = strchars "ABCDEFGHIJKLMNOPQRSTUVWXYZÄÖÜ"
  words$[] = [ "MAUS" "KATZE" "HUND" "ELEFANT" "PFERD" "NASHORN" "SPINNE" "AMEISE" "TIGER" "GEPARD" "SCHLANGE" "KUH" "SCHWEIN" "KROKODIL" "RATTE" "HUHN" "GIRAFFE" "SKORPION" "WAL" "DELFIN" "EIDECHSE" "FLIEGE" "RABE" "ADLER" "AMSEL" "SPECHT" "PINGUIN" "ESEL" "ZIEGE" "SCHAF" "ENTE" "SCHWAN" "MAULWURF" "BIENE" "WESPE" "ZECKE" "SCHNECKE" "FUCHS" "WOLF" "HAI" "FORELLE" "TAUBE" "BÄR" "DACHS" "EULE" "ERDMÄNNCHEN" "EICHHÖRNCHEN" "FROSCH" "FLEDERMAUS" "GOLDFISCH" "GEIER" "GANS" "GORILLA" "SCHIMPANSE" "HORNISSE" "HAMSTER" "HASE" "REH" "HIRSCH" "MURMELTIER" "IGEL" "KÄFER" "KAMEL" "KÄNGURU" "KREBS" "LÖWE" "LUCHS" "LAMA" "MOLCH" "NASHORN" "PAPAGEI" "PANDBÄR" "PAVIAN" "QUALLE" "REGENWURM" "SCHILDKRÖTE" "STINKTIER" "SEEPFERDCHEN" "ZEBRA" "BIBER" "ANTILOPE" "SEESTERN" "MUSCHEL" "CHAMÄLEON" "FAULTIER" "FLAMINGO" "KANINCHEN" "KOALABÄR" "KRÖTE" ]
else
  alpha$[] = strchars "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
  words$[] = [ "MOUSE" "CAT" "DOG" "ELEPHANT" "HORSE" "RHINO" "SPIDER" "ANT" "TIGER" "CHEETAH" "SNAKE" "COW" "PIG" "CROCODILE" "RAT" "MONKEY" "CHICKEN" "GIRAFFE" "SCORPION" "WHALE" "DOLPHIN" "LIZARD" "FLY" "RAVEN" "EAGLE" "BLACKBIRD" "WOODPECKER" "PENGUIN" "DONKEY" "GOAT" "SHEEP" "DUCK" "SWAN" "MOLE" "BEE" "WASP" "TICK" "SNAIL" "FOX" "WOLF" "SHARK" "TROUT" "DOVE" "BEAR" "BADGER" "OWL" "MEERKAT" "SQUIRREL" "FROG" "BAT" "GOLDFISH" "VULTURE" "GOOSE" "GORILLA" "CHIMPANZEE" "HORNET" "HAMSTER" "HARE" "DEER" "MARMOT" "HEDGEHOG" "BEETLE" "CAMEL" "KANGAROO" "CRAB" "LION" "LYNX" "LLAMA" "NEWT" "RHINO" "PARROT" "PANDA" "BABOON" "JELLYFISH" "EARTHWORM" "TURTLE" "SKUNK" "SEAHORSE" "ZEBRA" "BEAVER" "ANTELOPE" "STARFISH" "MUSSEL" "CHAMELEON" "SLOTH" "FLAMINGO" "RABBIT" "KOALA" "TOAD" ]
.
len sel[] len alpha$[]
# 
subr init
  for i range len sel[]
    sel[i] = 0
  .
  w$[] = strchars words$[random len words$[]]
  textsize 10
  game_stat = 0
.
background 000
func draw . .
  clear
  linewidth 1
  color 997
  for i range len w$[]
    move 4 + i * 8 93
    line 10 + i * 8 93
  .
  try = 0
  found = 0
  for ind range len sel[]
    r = ind div 5
    c = ind mod 5
    move 3 + c * 11 3 + r * 12
    color 040
    rect 10 11
    color 997
    k$ = alpha$[ind]
    if sel[ind] = 0
      move 4.5 + c * 11 4 + r * 12
      text k$
    else
      in = 0
      for pos range len w$[]
        if w$[pos] = k$
          in = 1
          move 4 + pos * 8 83
          text w$[pos]
          found += 1
        .
      .
      if in = 0
        try += 1
      .
    .
  .
  # 
  if try > 0
    move 65 60
    line 65 10
  .
  if try > 1
    line 85 10
  .
  if try > 2
    line 85 15
  .
  if try > 3
    move 85 20
    circle 6
    color 000
    circle 5
    color 997
  .
  if try > 4
    move 83 18
    circle 0.8
  .
  if try > 5
    move 87 18
    circle 0.8
  .
  if try > 6
    linewidth 1
    move 84 22
    line 86 22
  .
  if try > 7
    move 85 26
    line 85 40
  .
  if try > 8
    move 78 27
    line 85 30
  .
  if try > 9
    line 92 27
  .
  if try > 10
    move 78 47
    line 85 40
  .
  if try > 11
    line 92 47
    game_stat = 1
    timer 1
  .
  if found = len w$[]
    game_stat = 2
    timer 0.5
  .
.
on timer
  game_stat = 3
.
func search_letter . .
  c = (mouse_x - 3) div 11
  r = (mouse_y - 3) div 12
  ind = c + r * 5
  if ind < len sel[] and sel[ind] = 0
    sel[ind] = 1
    call draw
    if game_stat > 0
      for i range len w$[]
        move 4 + i * 8 83
        text w$[i]
      .
      move 60 70
      textsize 8
      if game_stat = 1
        text "SORRY!"
        # sound [ 330 0.2 294 0.2 262 0.4 ]
      else
        text "SUPER!"
        # sound [ 440 0.1 494 0.1 523 0.2 ]
      .
    .
  .
.
on mouse_down
  if game_stat = 0
    if mouse_x < 58 and mouse_x > 3 and mouse_y < 75 and mouse_y > 3
      call search_letter
    .
  elif game_stat = 3
    call init
    call draw
  .
.
call init
call draw

