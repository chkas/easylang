# Ice cream
#
sysconf topleft
if sysfunc "lang" = "de"
   alpha$[] = strchars "ABCDEFGHIJKLMNOPQRSTUVWXYZÄÖÜ"
   words$[] = [ "MAUS" "KATZE" "HUND" "ELEFANT" "PFERD" "NASHORN" "SPINNE" "AMEISE" "TIGER" "GEPARD" "SCHLANGE" "KUH" "SCHWEIN" "KROKODIL" "RATTE" "HUHN" "GIRAFFE" "SKORPION" "WAL" "DELFIN" "EIDECHSE" "FLIEGE" "RABE" "ADLER" "AMSEL" "SPECHT" "PINGUIN" "ESEL" "ZIEGE" "SCHAF" "ENTE" "SCHWAN" "MAULWURF" "BIENE" "WESPE" "ZECKE" "SCHNECKE" "FUCHS" "WOLF" "HAI" "FORELLE" "TAUBE" "BÄR" "DACHS" "EULE" "ERDMÄNNCHEN" "EICHHÖRNCHEN" "FROSCH" "FLEDERMAUS" "GOLDFISCH" "GEIER" "GANS" "GORILLA" "SCHIMPANSE" "HORNISSE" "HAMSTER" "HASE" "REH" "HIRSCH" "MURMELTIER" "IGEL" "KÄFER" "KAMEL" "KÄNGURU" "KREBS" "LÖWE" "LUCHS" "LAMA" "MOLCH" "NASHORN" "PAPAGEI" "PANDABÄR" "PAVIAN" "QUALLE" "REGENWURM" "SCHILDKRÖTE" "STINKTIER" "SEEPFERDCHEN" "ZEBRA" "BIBER" "ANTILOPE" "SEESTERN" "MUSCHEL" "CHAMÄLEON" "FAULTIER" "FLAMINGO" "KANINCHEN" "KOALABÄR" "KRÖTE" ]
else
   alpha$[] = strchars "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
   words$[] = [ "MOUSE" "CAT" "DOG" "ELEPHANT" "HORSE" "RHINO" "SPIDER" "ANT" "TIGER" "CHEETAH" "SNAKE" "COW" "PIG" "CROCODILE" "RAT" "MONKEY" "CHICKEN" "GIRAFFE" "SCORPION" "WHALE" "DOLPHIN" "LIZARD" "FLY" "RAVEN" "EAGLE" "BLACKBIRD" "WOODPECKER" "PENGUIN" "DONKEY" "GOAT" "SHEEP" "DUCK" "SWAN" "MOLE" "BEE" "WASP" "TICK" "SNAIL" "FOX" "WOLF" "SHARK" "TROUT" "DOVE" "BEAR" "BADGER" "OWL" "MEERKAT" "SQUIRREL" "FROG" "BAT" "GOLDFISH" "VULTURE" "GOOSE" "GORILLA" "CHIMPANZEE" "HORNET" "HAMSTER" "HARE" "DEER" "MARMOT" "HEDGEHOG" "BEETLE" "CAMEL" "KANGAROO" "CRAB" "LION" "LYNX" "LLAMA" "NEWT" "RHINO" "PARROT" "PANDA" "BABOON" "JELLYFISH" "EARTHWORM" "TURTLE" "SKUNK" "SEAHORSE" "ZEBRA" "BEAVER" "ANTELOPE" "STARFISH" "MUSSEL" "CHAMELEON" "SLOTH" "FLAMINGO" "RABBIT" "KOALA" "TOAD" ]
.
len sel[] len alpha$[]
#
subr init
   for i to len sel[] : sel[i] = 0
   w$[] = strchars words$[random len words$[]]
   game_stat = 0
.
proc ice x y col .
   gcolor col
   gcircle x y 3
   gcolor 432
   gpolygon [ x y + 8 x - 3.5 y + 1 x + 3.5 y + 1 ]
.
proc draw_ice try .
   if try < 11 : ice 72 57 900
   if try < 10 : ice 92 57 449
   if try < 9 : ice 82 52 990
   if try < 8 : ice 72 42 733
   if try < 7 : ice 92 42 950
   if try < 6 : ice 82 37 765
   if try < 5 : ice 72 27 096
   if try < 4 : ice 92 27 888
   if try < 3 : ice 82 22 080
   if try < 2 : ice 72 12 900
   if try < 1 : ice 92 12 990
.
gbackground 000
global gind .
proc drawkey ind col k$ .
   gtextsize 9
   r = (ind - 1) div 5
   c = (ind - 1) mod 5
   gcolor col
   grect c * 13 r * 14 12 13
   gcolor 997
   gtext 2 + c * 13 1.8 + r * 14 k$
.
proc draw .
   gclear
   glinewidth 1
   gcolor 997
   for i to len w$[]
      gline i * 8 - 6 98 i * 8 98
   .
   for ind to len sel[]
      k$ = alpha$[ind]
      if sel[ind] = 0
         drawkey ind 040 k$
      else
         drawkey ind 040 ""
         in = 0
         for pos to len w$[]
            if w$[pos] = k$
               in = 1
               gtext pos * 8 - 6 88 w$[pos]
               found += 1
            .
         .
         if in = 0 : try += 1
      .
   .
   draw_ice try
   gcolor 997
   if found = len w$[]
      game_stat = 2
      timer 1
   elif try > 10
      game_stat = 3
      timer 1
   .
.
proc smiley st .
   gcolor 983
   gcircle 80 73 7
   gcolor 000
   gcircle 77 71 1
   gcircle 83 71 1
   glinewidth 1
   if st = 2
      gcurve [ 77 75 80 79 83 75 ]
   else
      gcurve [ 77 77 80 74 83 77 ]
   .
.
proc sel .
   draw
   if game_stat > 1
      for i to len w$[]
         gtext i * 8 - 6 88 w$[i]
      .
      smiley game_stat
   .
.
on timer
   if game_stat = 1
      game_stat = 0
      sel
   elif game_stat >= 2
      game_stat = 4
   .
.
on mouse_up
   if gind <> -1 : drawkey gind 040 ""
.
proc search_letter .
   c = mouse_x div 13
   r = mouse_y div 14
   gind = c + r * 5 + 1
   if gind <= len sel[] and sel[gind] = 0
      sel[gind] = 1
      drawkey gind 060 alpha$[gind]
      game_stat = 1
      timer 0.5
   else
      gind = -1
   .
.
on mouse_down
   gind = -1
   if game_stat = 0
      if mouse_x < 65 and mouse_y < 84 : search_letter
   elif game_stat = 4
      init
      draw
   .
.
init
draw
