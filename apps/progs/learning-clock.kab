# Learning clock
#
proc draw hour min sec .
   gclear
   if hour < 10 : s$ = "0"
   s$ &= hour & ":"
   if min < 10 : s$ &= "0"
   s$ &= min & ":"
   if sec < 10 : s$ &= "0"
   s$ &= sec
   gtextsize 6
   gcolor -1
   gtext 69 3 s$
   # dial
   gcolor 333
   gcircle 50 50 45
   gcolor 797
   gcircle 50 50 44
   gcolor 333
   for i range0 60
      a = i * 6
      gcircle 50 + sin a * 40 50 + cos a * 40 0.25
   .
   gtextsize 7
   for i range0 12
      a = i * 30
      gcircle 50 + sin a * 40 50 + cos a * 40 1
      if i = 0
         gtext 45 47 + 35 12
      else
         gtext 48 + sin a * 35 47 + cos a * 35 i
      .
   .
   # hour
   glinewidth 2
   gcolor 000
   a = (hour * 60 + min) / 2
   gline 50 50 50 + sin a * 32 50 + cos a * 32
   # min
   glinewidth 1.5
   a = (sec + min * 60) / 10
   gline 50 50 50 + sin a * 40 50 + cos a * 40
   # sec
   glinewidth 1
   gcolor 700
   a = sec * 6
   gline 50 50 50 + sin a * 40 50 + cos a * 40
.
on timer
   if t <> floor systime
      t = floor systime
      h$ = timestr t
      sec = number substr h$ 18 2
      min = number substr h$ 15 2
      hour = number substr h$ 12 2
      draw hour min sec
   .
   timer 0.05
.
timer 0

