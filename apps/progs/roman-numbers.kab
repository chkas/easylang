# Roman numbers
#
global ans$ sol$ state ind cnt pts same .
proc disp col s$ .
   gcolor col
   grect 3 76 94 21
   gcolor 888
   gtextsize 12
   gtext 7 80 s$
.
proc disp2 col s$ .
   gcolor col
   grect 3 51 70 22
   gcolor 888
   gtextsize 12
   gtext 7 56 s$
.
func$ num2rom num .
   values[] = [ 1000 900 500 400 100 90 50 40 10 9 5 4 1 ]
   symbol$[] = [ "M" "CM" "D" "CD" "C" "XC" "L" "XL" "X" "IX" "V" "IV" "I" ]
   for i = 1 to len values[]
      while num >= values[i]
         rom$ &= symbol$[i]
         num -= values[i]
      .
   .
   return rom$
.
proc new_quest .
   repeat
      num = random 2500
      sol$ = num2rom num
      until len sol$ <= 12
   .
   ans$ = ""
   disp2 030 num
   disp 030 "_"
.
sym$[] = [ "M" "D" "C" "L" "X" "V" "I" ]
proc draw_key .
   h = ind
   if h <= 0
      s$ = "⌫"
   elif h <= 7
      s$ = sym$[ind]
   else
      s$ = "✓"
   .
   h += 3
   x = h mod 4 * 24 + 3
   y = 50 - h div 4 * 24 + 1
   grect x y 22 22
   gcolor 888
   gtextsize 16
   gtext x + 5 y + 5 s$
.
gbackground 010
proc init .
   state = 0
   cnt = 5
   pts = 0
   gclear
   for ind = 0 to 8
      gcolor 020
      draw_key
   .
   new_quest
   disp 030 "_"
.
proc handle_key .
   if ind = 8
      if ans$ = sol$
         cnt -= 1
         pts += 1 - same
         same = 0
         col = 050
         state = 1
         timer 2
      else
         same = 1
         ans$ = sol$
         col = 500
         state = 2
         timer 5
      .
   else
      if ind = 0
         ans$ = substr ans$ 1 (len ans$ - 1)
      else
         if len ans$ < 12
            ans$ &= sym$[ind]
         .
      .
      col = 030
   .
   h$ = ans$
   if state = 0 and len h$ < 12
      h$ &= "_"
   .
   disp col h$
.
proc handle_inp r c .
   if r = 0
      ind = 0
   else
      ind = (r - 1) * 4 + c + 1
   .
   gcolor 040
   draw_key
   handle_key
.
on mouse_down
   if state = 3
      ind = -1
      init
   else
      c = mouse_x div 25
      r = (75 - mouse_y) div 25
      if r < 0
         if ans$ = ""
            ind = -1
            disp 050 "New game"
            disp2 030 ""
            state = 4
            timer 2
         .
      elif state = 0
         handle_inp r c
      .
   .
.
on mouse_up
   if ind >= 0
      gcolor 020
      draw_key
      ind = -1
   .
.
proc smiley .
   gcolor 983
   gcircle 65 35 19
   gcolor 000
   gcircle 58 42 2
   gcircle 72 42 2
   glinewidth 2
   gcurve [ 58 28 65 22 72 28 ]
.
proc done .
   gtextsize 10
   gclear
   gcolor 888
   gtext 8 80 "Well done"
   gtext 8 64 pts & " Points"
   if pts = 5 : smiley
.
on timer
   if state = 1
      if cnt = 0
         state = 3
         timer 2
         done
      else
         new_quest
         state = 0
      .
   elif state = 2
      ans$ = ""
      disp 030 "_"
      state = 0
   elif state = 4
      init
   .
.
init
