# 1x1 trainer
#
len n[] 10
len m[] 10
subr init_vars
   n = 0
   m = 0
   state = 0
   sz = 10
   ind = -1
   t = 0
   row = -1
   res = 0
.
proc disp col s$ .
   gcolor col
   grect 3 76 94 21
   gcolor 888
   gtextsize 12
   gtext 7 82 s$
.
proc show_quest .
   j = random sz
   if row < 10
      n = n[j]
      n[j] = n[sz]
      n[sz] = n
      m = row
   else
      n = n[sz]
      m = m[sz]
   .
   sz -= 1
   disp 030 n & " × " & m & " = _"
   res = 0
.
proc sel_row .
   if ind <= 10
      t = systime
      row = ind
      if row = 0
         row = 11
      .
      if row < 10
         for i = 1 to 10
            n[i] = i
         .
      else
         for i = 1 to 10
            n[i] = random 8 + 1
            m[i] = random 8 + 1
         .
      .
      sz = 10
      show_quest
   .
.
proc draw_key .
   s$ = strchar (ind + 48)
   h = ind
   if h < 10 and h > 0
      h -= 1
      x = h mod 3 * 24 + 3
      y = 2 - h div 3
   else
      x = 3 * 24 + 3
      if h = 0
         y = 1
      elif h = 10
         y = 2
         s$ = "⌫"
      else
         y = 0
         s$ = "✓"
      .
   .
   y = y * 24 + 3
   grect x y 22 22
   gcolor 888
   gtextsize 16
   gtext x + 5 y + 4.5 s$
.
gbackground 010
proc init .
   init_vars
   gclear
   for ind = 0 to 11
      gcolor 020
      draw_key
   .
   ind = -1
   disp 030 "Which row?"
.
proc sel_inp .
   if ind = 11
      state = 1
      if n * m = res
         col = 050
         timer 0.5
      else
         sz += 1
         col = 500
         timer 3
      .
      h$ = n * m
   else
      if ind = 10
         res = res div 10
         h$ = res
         if res = 0 : h$ = "_"
      else
         if res >= 10 : res = 0
         res = res * 10 + ind
         h$ = res
      .
      col = 030
   .
   disp col n & " × " & m & " = " & h$
.
proc handle_key2 .
   if row = -1
      sel_row
   else
      sel_inp
   .
.
proc handle_key .
   c = mouse_x div 25
   r = 2 - mouse_y div 25
   if c < 3
      ind = r * 3 + c + 1
   else
      if r = 1
         ind = 0
      elif r = 0
         ind = 10
      else
         ind = 11
      .
   .
   gcolor 040
   draw_key
   handle_key2
.
on key_down
   ind = strcode keybkey - 48
   if state = 0
      if ind >= 0 and ind <= 9
         #
      elif ind = 21
         ind = 11
      elif ind = 18 or ind = 20
         ind = 10
      else
         ind = -1
      .
      if ind >= 0 : handle_key2
   elif state = 3
      init
   .
.
on mouse_down
   if state = 3
      init
   elif state = 0
      if mouse_y < 75
         handle_key
      elif res = 0
         init
      .
   .
.
on mouse_up
   if ind >= 0
      gcolor 020
      draw_key
   .
.
proc smiley t .
   if t <= 60
      gcolor 983
      gcircle 65 35 19
      gcolor 000
      gcircle 58 42 2
      gcircle 72 42 2
      glinewidth 2
      if t <= 30
         gcurve [ 56 32 65 19 74 32 ]
      else
         gcurve [ 58 28 65 22 72 28 ]
      .
   .
.
proc done .
   t = floor (systime - t)
   gcolor 030
   grect 0 0 100 100
   gcolor 888
   gtextsize 10
   gtext 10 82 "Well done!"
   gtext 10 66 "Time: " & t
   s$ = "All"
   if row < 10 : s$ = row
   gtextsize 6
   gtext 10 10 "Row: " & s$
   smiley t
.
on timer
   if state = 2
      state = 3
   elif sz = 0
      state = 2
      ind = -1
      timer 2
      done
   else
      show_quest
      state = 0
   .
.
init
