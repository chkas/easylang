# Paint
#
sysconf topleft
col[] = [ 900 090 009 990 620 000 888 ]
thick[] = [ 1 2 4 8 ]
sel_thick = 3
sel_col = 1
state = 0
down = 0
#
proc showpen .
   gcolor 444
   grect 0 0 100 8
   gcolor 834
   grect 2 2 4 2
   gcolor 247
   grect 2 4 4 2
   for i = 1 to len col[]
      gcolor col[i]
      sz = 3
      if i = sel_col : sz = 4
      gcircle i * 8 + 4 4 sz
   .
   for i = 1 to len thick[]
      gcolor 555
      if i = sel_thick : gcolor col[sel_col]
      gcircle 64 + i * 8 4 i
   .
   gcolor col[sel_col]
   glinewidth thick[sel_thick]
.
c[] = [ 9 0 0 ]
proc picker .
   col = col[sel_col]
   for i = 2 downto 0
      c[i + 1] = col mod 10
      col = col div 10
   .
   for i = 0 to 9
      f = 100
      for j = 0 to 2
         gcolor i * f
         grect 10 + i * 8 16 + j * 6.6 8 6.6
         f = f div 10
      .
   .
   gcolor 666
   for i = 0 to 2
      gcircle c[i + 1] * 8 + 14 16 + i * 6.6 + 3.3 1
   .
.
global mx my .
proc action x y .
   if state = 0
      if y < 8
         if x < 8
            state = 1
            gbackground -1
            picker
         elif x < 64
            sel_col = x div 8
         elif x > 68 and x < 96
            sel_thick = (x - 68) div 8 + 1
         .
         showpen
      else
         down = 1
         gline x y x y
         mx = x
         my = y
      .
   else
      if y < 8 and x > 8 and x < 64
         sel_col = x div 8
         showpen
         picker
      elif y > 16 and y < 35.8 and x > 10 and x < 90
         i = floor ((y - 16) / 6.6)
         c[i + 1] = floor (x - 10) div 8
         col[sel_col] = c[1] * 100 + c[2] * 10 + c[3]
         showpen
         picker
      else
         state = 0
         gclear
         showpen
      .
   .
.
on mouse_down
   action mouse_x mouse_y
.
on mouse_up
   down = 0
.
on mouse_move
   if down = 1 and mouse_y > 8 + thick[sel_thick] / 2
      gline mx my mouse_x mouse_y
      mx = mouse_x
      my = mouse_y
   .
.
gcolor 888
grect 0 0 100 100
showpen

