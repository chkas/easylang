<!doctype html>
<html lang=en>
<meta charset=utf-8><title>Christmas tree</title>
<meta name="viewport" content="width=device-width,initial-scale=1.0">
<meta name="description" content="Programming in Easylang">
<link rel="icon" href="/icon.png" type="image/x-png">

<div id=tut><b>Loading ...</b></div>
<script src=easy_code2.js></script>
<script src=easy_tut2x.js></script>

<script>

txt_tutor=`* Drawing a Christmas tree

##
gcircle 10 10 2

+ It's Christmas - we need a Christmas tree.

+ First, we draw the tree trunk, which is easy. It is a rectangle at position *47, 10* with a width of *6* and a height of *10*. The color is *421*, a combination of 4 parts red, 2 green, and 1 blue.

+ The green branches of the Christmas tree are shown as green triangles. *gpolygon* expects the x and y values of the corner points alternately.

drawgrid
# trunk
gcolor 421
grect 47 10 6 10
#
y = 30
w = 20
# branch
gcolor 050
gpolygon [ 50 (y + 20) (50 - w) y (50 + w) y ]

+ *drawgrid* helps with orientation during development.

+ We place these triangles on top of each other, with them becoming narrower towards the top.

drawgrid
y = 10
gcolor 421
grect 50 - 3 10 6 10
#
w = 30
y = 20
gcolor 050
while y < 70
   gpolygon [ 50 (y + 20) (50 - w) y (50 + w) y ]
   y += 12
   w -= 4
.

+ Now it gets a little more complicated. We want to draw a 5-pointed star. To do this, we need trigonometric functions, since the points are evenly spaced around a circle. Let's try drawing it using lines. We move twice around the circle, increasing the angle by 144 degrees each time.

x = 20
y = 70
while deg <= 720
   x += cos deg * 40
   y += sin -deg * 40
   glineto x y
   deg += 144
   sleep 0.2
.

+ If we collect these corner points in an array and then draw the polygon, we have our yellow star. It's best to create a procedure for this right away.

proc star x y sz .
   x -= sz / 2
   while deg < 720
      x += cos deg * sz
      y += sin -deg * sz
      f[] &= x
      f[] &= y
      deg += 144
   .
   gcolor 990
   gpolygon f[]
.
star 40 80 40

+ Now we can decorate the tree with red glass balls and yellow stars. We arrange the decorations alternately to the right or left of the center.

proc star x y sz .
   x -= sz / 2
   while deg < 720
      x += cos deg * sz
      y += sin -deg * sz
      f[] &= x
      f[] &= y
      deg += 144
   .
   gcolor 990
   gpolygon f[]
.
proc tree .
   # tree trunk
   gcolor 421
   grect 50 - 3 10 6 10
   w = 30
   y = 20
   d = 1
   while y < 70
      # green branch
      gcolor 050
      gpolygon [ 50 (y + 20) (50 - w) y (50 + w) y ]
      # tree ball and star
      star 50 + d * w / 2 y + 6 4
      gcolor 900
      gcircle 50 + d * w / 5 y + 8 2
      w -= 4
      d *= -1
      y += 12
   .
   star 50 88 10
.
tree

+ Now add the candles. A candle is a rectangle with a yellow circle on top.

proc candle x y .
   gcolor 800
   grect x - 1 y 2 4
   gcolor 990
   gcircle x y + 5 1
.
proc draw_candles .
   y = 28
   d = -1
   w = 10
   while y < 80
      candle 50 + d * w y
      d *= -1
      w -= 2
      y += 12
   .
.
draw_candles

+ And the Christmas presents, of course.

proc present x y sz col .
   half = sz / 2
   band = sz * 0.12
   gcolor col
   grect x - half y sz sz
   gcolor 555
   grect x - band / 2 y band sz
   grect x - half y + half - band / 2 sz band
   glinewidth band * 0.8
   bowy = y + sz + band
   boww = sz * 0.3
   gline x bowy - band x - boww bowy
   gline x bowy - band x + boww bowy
.
present 37 30 9 900
present 30 28 10 009
present 60 28 9 990

+ Now everything together.

proc present x y sz col .
   half = sz / 2
   band = sz * 0.12
   gcolor col
   grect x - half y sz sz
   gcolor 555
   grect x - band / 2 y band sz
   grect x - half y + half - band / 2 sz band
   glinewidth band * 0.8
   bowy = y + sz + band
   boww = sz * 0.3
   gline x bowy - band x - boww bowy
   gline x bowy - band x + boww bowy
.
present 37 10 9 900
present 30 8 10 009
present 60 8 9 990
#
proc star x y sz .
   x -= sz / 2
   while deg < 720
      x += cos deg * sz
      y += sin -deg * sz
      f[] &= x
      f[] &= y
      deg += 144
   .
   gcolor 990
   gpolygon f[]
.
proc tree .
   gcolor 421
   grect 50 - 3 10 6 10
   w = 30
   y = 20
   d = 1
   while y < 70
      gcolor 050
      gpolygon [ 50 (y + 20) (50 - w) y (50 + w) y ]
      star 50 + d * w / 2 y + 6 4
      gcolor 900
      gcircle 50 + d * w / 5 y + 8 2
      w -= 4
      d *= -1
      y += 12
   .
   star 50 88 10
.
tree
#
proc candle x y .
   gcolor 800
   grect x - 1 y 2 4
   gcolor 990
   gcircle x y + 5 1
.
proc draw_candles .
   y = 28
   d = -1
   w = 10
   while y < 80
      candle 50 + d * w y
      d *= -1
      w -= 2
      y += 12
   .
.
draw_candles

+ The candles should flicker. For example, we could redraw the candles five times per second and make the flame darker every fifth time. However, this looks unnatural when the candles flicker in time. It is better to draw the flame darker with a probability of 20 percent. We do this with *randomf* in the event function *on timer*.

on timer
   gcolor 800
   grect 20 60 6 10
   gcolor 990
   if randomf < 0.2 : gcolor 660
   gcircle 23 73 3
   timer 0.1
.
timer 0

* Happy Christmas

proc present x y sz col .
   half = sz / 2
   band = sz * 0.12
   gcolor col
   grect x - half y sz sz
   gcolor 555
   grect x - band / 2 y band sz
   grect x - half y + half - band / 2 sz band
   glinewidth band * 0.8
   bowy = y + sz + band
   boww = sz * 0.3
   gline x bowy - band x - boww bowy
   gline x bowy - band x + boww bowy
.
present 37 10 9 900
present 30 8 10 009
present 60 8 9 990
#
proc star x y sz .
   x -= sz / 2
   while deg < 720
      x += cos deg * sz
      y += sin -deg * sz
      f[] &= x
      f[] &= y
      deg += 144
   .
   gcolor 990
   gpolygon f[]
.
proc tree .
   gcolor 421
   grect 50 - 3 10 6 10
   w = 30
   y = 20
   d = 1
   while y < 70
      gcolor 050
      gpolygon [ 50 (y + 20) (50 - w) y (50 + w) y ]
      star 50 + d * w / 2 y + 6 4
      gcolor 900
      gcircle 50 + d * w / 5 y + 8 2
      w -= 4
      d *= -1
      y += 12
   .
   star 50 88 10
.
tree
#
proc candle x y .
   gcolor 800
   grect x - 1 y 2 4
   gcolor 990
   if randomf < 0.2 : gcolor 660
   gcircle x y + 5 1
.
proc draw_candles .
   y = 28
   d = -1
   w = 10
   while y < 80
      candle 50 + d * w y
      d *= -1
      w -= 2
      y += 12
   .
.
draw_candles
on timer
   draw_candles
   timer 0.1
.
timer 0
`

function hook() {
	var dom = window.location.host
	tut.insertAdjacentHTML("beforeend", `
<p><hr>
<p><a href=/ide/>Programming IDE<a>
<p><a href=/programming_basics.html>Programming Course<a>
<p><a href=/apps/>About Easylang<a>
<p><a href=https://github.com/chkas/easylang>Source on Github<a>
<p><hr>
<small>christof.kaser@gmail.com</small>
`)
	var srcx = `
proc present x y sz col .
   half = sz / 2
   band = sz * 0.12
   gcolor col
   grect x - half y sz sz
   gcolor 555
   grect x - band / 2 y band sz
   grect x - half y + half - band / 2 sz band
   glinewidth band * 0.8
   bowy = y + sz + band
   boww = sz * 0.3
   gline x bowy - band x - boww bowy
   gline x bowy - band x + boww bowy
.
present 37 10 9 900
present 30 8 10 009
present 60 8 9 990
#
proc star x y sz .
   x -= sz / 2
   while deg < 720
      x += cos deg * sz
      y += sin -deg * sz
      f[] &= x
      f[] &= y
      deg += 144
   .
   gcolor 990
   gpolygon f[]
.
proc tree .
   y = 10
   gcolor 421
   grect 50 - 3 y 6 10
   #
   w = 30
   y += 10
   d = 1
   while y < 70
      gcolor 050
      gpolygon [ 50 (y + 20) (50 - w) y (50 + w) y ]
      star 50 + d * w / 2 y + 6 4
      gcolor 900
      gcircle 50 + d * w / 5 y + 8 2
      w -= 4
      d *= -1
      y += 12
   .
   star 50 88 10
.
tree
#
proc candle x y .
   gcolor 800
   grect x - 1 y 2 4
   gcolor 990
   if randomf < 0.2 : gcolor 660
   gcircle x y + 5 1
.
proc draw_candles .
   y = 28
   d = -1
   w = 10
   while y < 80
      candle 50 + d * w y
      d *= -1
      w -= 2
      y += 12
   .
.
draw_candles
on timer
   draw_candles
   timer 0.1
.
timer 0
`
	var canvx = document.querySelector('canvas')
	easyrun(srcx, canvx)
}
</script>
</html>


