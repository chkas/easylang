<!doctype html>
<meta charset=utf-8><title>Symmetric Cipher</title>
<meta name="viewport" content="width=device-width,initial-scale=1.0">
<meta name="description" content="symmetric encryption, speck, block modes, brute-force, nonce reuse">
<link rel="icon" href="../icon.png" type="image/x-png">

<div id=tut><b>Loading ...</b></div>
<script src=easy_code2.js></script>
<script src=easy_tut2.js></script>

<script>

txt_tutor=`* Symmetric Cipher

+ Symmetric ciphers use the same key (generally a very large number) 
to encrypt and decrypt the data. This key must be kept secret.  

* XOR Encryption

+ XOR encryption with a repeating key is simple, but not secure.

proc str2arr s$ . ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1
      ar[] &= 0
   .
.
proc arr2str ar[] . s$ .
   s$ = ""
   for n in ar[]
      for i = 1 to 4
         s$ &= strchar n mod 256
         n = n div 256
      .
   .
.
# 
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_block . x y .
   for i = 1 to 26
      rot spkb[i] x y
   .
.
proc rotrev k . x y .
   y = bitxor y x
   y = bitor bitshift y -3 bitshift y 29
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitor bitshift x 8 bitshift x -24
.
proc sp_blockrev . x y .
   for i = 26 downto 1
      rotrev spkb[i] x y
   .
.
# 
proc sp_encr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_block x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_blockrev x y
      out[] &= x
      out[] &= y
   .
.
msg$ = "Hi Alice. We have a surprise birthday party for Trudy. Bob"
str2arr msg$ msg[]
# 
secrkey = 18238004
# 
sp_encr secrkey msg[] encr[]
print encr[]
print ""
sp_decr secrkey encr[] r[]
arr2str r[] s$
print s$
#  
# 
print ""
print "ECB demo:"
msg$ = "Hi Bob! Hi Joe! Hi Bob! "
str2arr msg$ msg[]
sp_encr 1234567890 msg[] encr[]
print encr[]

* Cracking XOR

+ If data is repeatedly encrypted with the same
XOR key, the encryption can be easily cracked: 
The partial keys, e.g. always 8 bits, that encrypt
a character are varied independently until they 
"fit" the data they decrypt - a text, for example, 
contains many letters and spaces. 

proc arr2str ar[] . s$ .
   s$ = ""
   for n in ar[]
      for i = 1 to 4
         s$ &= strchar n mod 256
         n = n div 256
      .
   .
.
proc xorcrypt key in[] . out[] .
   out[] = [ ]
   for i = 1 to len in[]
      out[] &= bitxor in[i] key
   .
.
# 
proc crack_xor in[] . .
   len k[] 4
   for ki = 1 to 4
      for b = 0 to 255
         k[ki] = b
         key = k[1] + 0x100 * k[2] + 0x10000 * k[3] + 0x1000000 * k[4]
         xorcrypt key in[] out[]
         m = 0
         for i = 1 to len out[] - 1
            h = (bitshift out[i] ((ki - 1) * -8)) mod 256
            if h >= 97 and h <= 122 or h >= 65 and h <= 90 or h = 32 or h = 46
               m += 1
            elif h > 127 or h < 31
               m -= 20
            .
         .
         if m >= len out[] * 4 div 5
            print "Partial key " & ki & " : " & b
            xorcrypt key in[] out[]
            arr2str out[] s$
            print s$
            print ""
            break 1
         .
      .
   .
   print "Key: " & key
.
encr[] = [ 3493208956 4101317464 4097927706 3883344404 4251347537 4101514325 2977393232 3781443399 4100268870 3816761364 4034011712 4033243725 2976857670 2976130386 4116920416 4183976525 4063639901 4286523227 4031081043 4116987712 4097192789 4168625986 3208129882 4084795412 ]
crack_xor encr[]
# 

* Block Cipher

+ *AES* is the most commonly used block cipher. The block size is 128 bits and the key size is usually 128 bits. It is somewhat complicated for micro-controllers and for teaching purposes.

+ The rather unknown *Speck* is well suited for these purposes. This is also a block cipher with different block sizes and key lengths. We use it here for demo purposes with a 64-bit block size, with a 64-bit key length (which is not specified for this block size) and 26 rounds. (The key length is reduced to 53 bits by using a double variable).

+ Blocks of 8 bytes are encrypted together. If two blocks have the same input, they give the same output. This is called *Electronic Codebook (ECB)* mode. To prevent this, the output of one cipher can be combined with the input of the next cipher (*Cipher Block Chaining*).

+ Encryption is done by the basic operations rotation, XOR and addition with the key as parameter in several rounds. For decryption, these steps must be performed in reverse order.

proc str2arr s$ . ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1
      ar[] &= 0
   .
.
proc arr2str ar[] . s$ .
   s$ = ""
   for n in ar[]
      for i = 1 to 4
         s$ &= strchar n mod 256
         n = n div 256
      .
   .
.
# 
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
proc sp_prepkey0 a b . .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc sp_block . x y .
   for i = 1 to 26
      rot spkb[i] x y
   .
.
proc rotrev k . x y .
   y = bitxor y x
   y = bitor bitshift y -3 bitshift y 29
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitor bitshift x 8 bitshift x -24
.
proc sp_blockrev . x y .
   for i = 26 downto 1
      rotrev spkb[i] x y
   .
.
# 
proc sp_encr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_block x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_blockrev x y
      out[] &= x
      out[] &= y
   .
.
msg$ = "Hi Alice. We have a surprise birthday party for Trudy. Bob"
str2arr msg$ msg[]
# 
secrkey = 18238004
# 
sp_encr secrkey msg[] encr[]
print encr[]
print ""
sp_decr secrkey encr[] r[]
arr2str r[] s$
print s$
#  
# 
print ""
print "ECB demo:"
msg$ = "Hi Bob! Hi Joe! Hi Bob! "
str2arr msg$ msg[]
sp_encr 1234567890 msg[] encr[]
print encr[]

* Cypher Block Chaining

+ Before a block is encrypted, it is XORed with the last encrypted block. The 
initialization vector *IV* is used for the first block. After decrypting a 
block, the same XOR operation must be applied.

+ Equal input blocks do not lead to equal output blocks. But if the blocks at 
the beginning are equal, the encryption in these blocks is also equal. 
This can be avoided by using a random IV to which the first block is 
XORed. This IV must be transmitted (or stored) together with the ciphertext. 
Conveniently, it is added as the first block.

proc str2arr s$ . ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1
      ar[] &= 0
   .
.
proc arr2str ar[] . s$ .
   s$ = ""
   for n in ar[]
      for i = 1 to 4
         s$ &= strchar n mod 256
         n = n div 256
      .
   .
.
# 
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
proc sp_prepkey0 a b . .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc sp_block . x y .
   for i = 1 to 26
      rot spkb[i] x y
   .
.
proc rotrev k . x y .
   y = bitxor y x
   y = bitor bitshift y -3 bitshift y 29
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitor bitshift x 8 bitshift x -24
.
proc sp_blockrev . x y .
   for i = 26 downto 1
      rotrev spkb[i] x y
   .
.
# 
proc sp_encr key . in[] out[] .
   sp_prepkey key
   x = random 0x100000000
   y = random 0x100000000
   out[] = [ x y ]
   for i = 1 step 2 to len in[]
      x = bitxor in[i] x
      y = bitxor in[i + 1] y
      sp_block x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 3 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_blockrev x y
      out[] &= bitxor x in[i - 2]
      out[] &= bitxor y in[i - 1]
   .
.
msg$ = "Hi Alice. We have planned a surprise birthday party for Trudy this coming Saturday evening. Bob"
str2arr msg$ msg[]
# 
secrkey = 18238004
# 
sp_encr secrkey msg[] encr[]
print encr[]
print ""
sp_decr secrkey encr[] r[]
arr2str r[] s$
print s$
#  
# 
print ""
print "CBC demo:"
msg$ = "Hi Bob! Hi Joe! Hi Bob! "
str2arr msg$ msg[]
sp_encr 1234567890 msg[] encr[]
print encr[]

* Brute Force

+ Every encryption can be cracked by trying all keys. With a key length of 53 bits, that is about 9000 trillion attempts, which is doable with some effort. With 128 bit key length, it would take many billions of years even with the fastest parallel hardware.

+ For a brute force attack, you don't need to know the result you are looking for. The key is the correct one if the decrypted data matches the expected format. In this example, if the decrypted data is all ASCII characters.

+ To shorten the search time in this example, we start the key search near the key.

proc arr2str ar[] . s$ .
   s$ = ""
   for n in ar[]
      for i = 1 to 4
         s$ &= strchar n mod 256
         n = n div 256
      .
   .
.
# 
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
# 
proc sp_prepkey0 a b . .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc rotrev k . x y .
   y = bitxor y x
   y = bitor bitshift y -3 bitshift y 29
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitor bitshift x 8 bitshift x -24
.
proc sp_blockrev . x y .
   for i = 26 downto 1
      rotrev spkb[i] x y
   .
.
proc sp_decr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 3 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_blockrev x y
      out[] &= bitxor x in[i - 2]
      out[] &= bitxor y in[i - 1]
   .
.
# 
proc istext out[] . ok .
   ok = 0
   for n in out[]
      for j = 1 to 4
         b = n mod 256
         n = n div 256
         if b > 1 and b < 32 or b > 126
            return
         .
      .
   .
   ok = 1
   return
.
proc brute_force start in[] . key .
   print "Brute forcing"
   in0[] = [ in[1] in[2] in[3] in[4] ]
   for key = start to 9007199254740991
      if key mod 50000 = 0
         if key mod 1000000 = 0
            print ""
            write key & ": "
         .
         write "."
      .
      sp_decr key in0[] out[]
      istext out[] ok
      if ok = 1
         sp_decr key in[] out[]
         istext out[] ok
         if ok = 1
            print ""
            return
         .
      .
   .
.
encr[] = [ 2995282530 3231593157 2262435520 1455743690 2239111503 754144163 1776451451 2619565752 542475955 2326252767 4024366929 1202124251 2876779313 2374443044 3390291199 3315251173 2771012052 2469472958 3231745358 948080008 3092188112 2968078269 521194386 1697615974 659159096 2703243526 ]
#
brute_force 17000000 encr[] key
print "key: " & key
sp_decr key encr[] r[]
arr2str r[] s$
print s$

+ Who can crack this secret message?

~ [ 1576593714 3827464596 2280490437 3161296180 4078846994 3831482868 1030420123 3080413550 3292128070 1083128452 33181364 296910390 1665490072 3003556078 ]

* Key generation

+ We can use our symmetric cipher to generate a 64-bit output from an input of any length. To do this, we encrypt the output one by one using each 64-bit part of the input as the key, starting at 0. This is a cryptographic hash. 

+ We use this to generate a 53-bit key from a variable-length text key. The data is then encrypted with our easy-to-use block cipher in CBC mode - which doesn't go wrong as easily as stream ciphers.

proc str2arr s$ . ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1
      ar[] &= 0
   .
.
proc arr2str ar[] . s$ .
   s$ = ""
   for n in ar[]
      for i = 1 to 4
         s$ &= strchar n mod 256
         n = n div 256
      .
   .
.
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
proc sp_prepkey0 a b . .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc sp_block . x y .
   for i = 1 to 26
      rot spkb[i] x y
   .
.
proc rotrev k . x y .
   y = bitxor y x
   y = bitor bitshift y -3 bitshift y 29
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitor bitshift x 8 bitshift x -24
.
proc sp_blockrev . x y .
   for i = 26 downto 1
      rotrev spkb[i] x y
   .
.
proc sp_encr key . in[] out[] .
   sp_prepkey key
   x = random 0x100000000
   y = random 0x100000000
   out[] = [ x y ]
   for i = 1 step 2 to len in[]
      x = bitxor in[i] x
      y = bitxor in[i + 1] y
      sp_block x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 3 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      sp_blockrev x y
      out[] &= bitxor x in[i - 2]
      out[] &= bitxor y in[i - 1]
   .
.
func genkey s$ .
   str2arr s$ b[]
   for i = 1 step 2 to len b[] - 1
      xp = x
      yp = y
      sp_prepkey0 b[i] b[i + 1]
      sp_block x y
      x = bitxor x xp
      y = bitxor y yp
   .
   # make a 53 bit value for the "double" variable
   return x + y mod 2097152 * 0x100000000
.
# 
msg$ = "Hello Alice. We're planning a surprise party for Trudy next weekend. Kind regards, Bob"
str2arr msg$ msg[]
sp_encr genkey "That is a very long password!!" msg[] encr[]
print encr[]
print ""
sp_decr genkey "That is a very long password!!" encr[] r[]
arr2str r[] s$
print s$

* Counter Mode

+ If you encrypt a continuous counter with a block cipher, you 
get a bit sequence that cannot be predicted without knowing the 
key. With this sequence then the input data is XORed and you 
get a stream cipher. 

+ The counter must not occur more than once for the same key, 
otherwise the encryption is vulnerable. The counter does not 
have to be kept secret. It can be transmitted together with 
the encrypted data. 

+ The start counter is also called *Nonce* ("Number Once"). The 
actual counter can also be combined with a start value in a 
different way (XOR, concatenate).

proc bytes2str ar[] . s$ .
   s$ = ""
   for b in ar[]
      s$ &= strchar b
   .
.
proc str2bytes s$ . b[] .
   b[] = [ ]
   for c$ in strchars s$
      b[] &= strcode c$
   .
.
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
proc sp_prepkey0 a b . .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc sp_block . x y .
   for i = 1 to 26
      rot spkb[i] x y
   .
.
proc sp_ctrmode key ctr . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 to len in[]
      m = (i - 1) mod 8
      if m = 0
         dx = ctr mod 0x100000000
         dy = ctr div 0x100000000
         call sp_block dx dy
         ctr += 1
      .
      if m <= 3
         h = (bitshift dx (-8 * m)) mod 255
      else
         m -= 4
         h = (bitshift dy (-8 * m)) mod 255
      .
      out[] &= bitxor in[i] h
   .
.
msg$ = "Hi Alice. We have a surprise birthday party for Trudy. Bob"
call str2bytes msg$ msg[]
# 
secrkey = 18238004
counter = 1423
# 
call sp_ctrmode secrkey counter msg[] encr[]
print encr[]
print ""
call sp_ctrmode secrkey counter encr[] r[]
call bytes2str r[] s$
print s$

* The counter is important in counter mode.

+ Bob and Alice exchange secret messages. A fatal mistake 
happens to them doing so.

proc str2bytes s$ . b[] .
   b[] = [ ]
   for c$ in strchars s$
      b[] &= strcode c$
   .
.
proc rot k . x y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitxor x k
   y = bitor bitshift y 3 bitshift y -29
   y = bitxor y x
.
global spkb[] .
proc sp_prepkey0 a b . .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key . .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc sp_block . x y .
   for i = 1 to 26
      rot spkb[i] x y
   .
.
proc sp_ctrmode key ctr . in[] out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 to len in[]
      m = (i - 1) mod 8
      if m = 0
         dx = ctr mod 0x100000000
         dy = ctr div 0x100000000
         call sp_block dx dy
         ctr += 1
      .
      if m <= 3
         h = (bitshift dx (-8 * m)) mod 255
      else
         m -= 4
         h = (bitshift dy (-8 * m)) mod 255
      .
      out[] &= bitxor in[i] h
   .
.
# 
secrkey = 418785432574
counter = 0
# 
msg$[] &= "Hi Alice. We are going to have a surprise birthday party for Trudy. Bob"
msg$[] &= "Hi Bob! Great. Hopefully she won't be able to decode the message. Alice"
msg$[] &= "Hi Alice! We use encryption. It takes millions of years to crack it. Bob"
msg$[] &= "Hi Bob! Yes, but you know Trudy is very smart about these things. Alice"
msg$[] &= "Hi Alice! Yes I know. But trust me. This time nothing will go wrong. Bob"
msg$[] &= "Hi Bob. Well, what do you say we meet at the mall tomorrow midday? Alice"
msg$[] &= "Hi Alice. Good idea. Well then see you tomorrow. Have a nice evening. Bob"
# 
for i = 1 to len msg$[]
   str2bytes msg$[i] msg[]
   sp_ctrmode secrkey counter msg[] encr[]
   print encr[]
   print ""
.

+ Trudy can intercept the encrypted messages and tests if she can 
find a common bit sequence for the different messages that decrypt 
the messages. She uses a letter frequency analysis for this.

c[][] &= [ 248 72 148 136 246 28 65 115 127 19 80 10 201 190 92 35 237 230 202 117 93 185 166 189 125 216 11 173 94 139 131 159 85 107 132 101 175 22 104 25 234 52 30 40 158 56 133 149 62 87 230 7 201 202 233 129 252 30 211 255 38 20 167 34 77 6 217 172 216 8 50 ]
c[][] &= [ 248 72 148 139 245 23 3 54 22 65 98 14 157 241 14 14 162 241 192 122 70 178 234 176 50 139 11 169 8 153 204 144 82 108 209 117 186 68 96 8 227 113 92 53 131 108 137 148 60 65 162 18 136 204 245 157 252 21 217 254 117 33 178 50 7 95 182 224 243 4 53 ]
c[][] &= [ 248 72 148 136 246 28 65 115 112 19 80 10 201 170 93 35 237 228 203 127 65 167 246 189 123 151 13 226 8 167 215 222 1 121 154 114 172 68 108 3 227 120 21 46 130 63 205 158 57 14 191 18 201 202 238 216 168 23 156 238 116 33 182 60 9 22 131 162 186 37 63 93 ]
c[][] &= [ 248 72 148 139 245 23 3 54 8 86 116 67 201 189 91 50 237 248 202 105 19 181 232 166 101 216 55 190 93 138 218 222 28 107 209 97 186 22 120 74 252 121 29 51 152 108 140 147 48 91 178 87 220 208 248 139 185 88 200 229 111 46 178 36 7 95 182 224 243 4 53 ]
c[][] &= [ 248 72 148 136 246 28 65 115 112 19 94 10 154 255 103 102 166 239 202 107 29 254 196 188 102 216 23 190 93 157 215 222 24 125 223 55 139 12 104 25 175 96 21 44 137 108 131 158 43 70 175 25 207 152 234 145 176 20 156 234 105 96 162 37 70 17 144 162 186 37 63 93 ]
c[][] &= [ 248 72 148 139 245 23 12 54 6 86 107 3 197 255 89 46 172 245 133 120 92 254 255 166 103 216 16 173 81 206 212 155 85 117 148 114 171 68 96 30 175 96 20 36 204 33 140 157 51 14 178 24 197 215 239 138 179 15 156 224 111 36 177 54 80 64 215 205 246 14 51 90 ]
c[][] &= [ 248 72 148 136 246 28 65 115 127 19 64 0 134 187 14 47 169 228 196 50 19 137 227 165 126 216 23 164 77 128 131 141 16 125 209 110 176 17 33 30 224 121 19 51 158 35 154 223 127 102 167 1 205 152 252 216 178 17 223 232 38 37 163 50 71 22 153 235 180 71 18 80 150 ]
#  
proc bytes2str ar[] . s$ .
   s$ = ""
   for b in ar[]
      s$ &= strchar b
   .
.
proc crack_xor . key[] .
   key[] = [ ]
   for pos = 1 to 66
      max = 1
      maxcnt = 0
      for b = 0 to 0xff
         out[] = [ ]
         for i = 1 to len c[][]
            out[] &= bitxor c[i][pos] b
         .
         m = 0
         for o in out[]
            o$ = strchar o
            if o >= 65 and o <= 90
               m += 3
            elif o >= 97 and o <= 122
               m += 6
            elif o < 32
               m -= 5
            .
            if o$ = "e"
               m += 3
            elif o$ = "a" or o$ = "t"
               m += 2
            elif o$ = "n" or o$ = "o" or o$ = "r" or o$ = "s" or o$ = "i" or o$ = "h" or o$ = "i"
               m += 1
            elif o$ = " "
               m += 9
            elif o$ = "."
               m += 6
            elif o$ = "k" or o$ = "v" or o$ = "b"
               m -= 1
            elif o$ = "z" or o$ = "x" or o$ = "q" or o$ = "j"
               m -= 2
            .
         .
         if m >= max
            maxcnt += 1
            if m > max
               maxcnt = 1
               max = m
               maxb = b
            .
         .
      .
      if maxcnt = 1
         key[] &= maxb
      else
         key[] &= -1
      .
   .
.
crack_xor key[]
# 
proc decrypt encr[] key[] . .
   out[] = [ ]
   for i = 1 to len key[]
      if key[i] <> -1
         out[] &= bitxor encr[i] key[i]
      else
         out[] &= strcode "_"
      .
   .
   bytes2str out[] s$
   print s$
   print ""
.
for i = 1 to len c[][]
   decrypt c[i][] key[]
.

+ That worked. Alice and Bob obviously messed up and generated the 
same XOR bit sequence in a stream cipher for all messages - they 
didn't know what the counter was for, so they just set it to 0.

+ The first and last characters of the messages could not be 
decoded, because they differed too little between the messages.

+ Trudy can complete the first message even further without a 
computer and thus also determine the XOR key sequence and thus 
also decode the further messages almost completely.

c[][] &= [ 248 72 148 136 246 28 65 115 127 19 80 10 201 190 92 35 237 230 202 117 93 185 166 189 125 216 11 173 94 139 131 159 85 107 132 101 175 22 104 25 234 52 30 40 158 56 133 149 62 87 230 7 201 202 233 129 252 30 211 255 38 20 167 34 77 6 217 172 216 8 50 ]
c[][] &= [ 248 72 148 139 245 23 3 54 22 65 98 14 157 241 14 14 162 241 192 122 70 178 234 176 50 139 11 169 8 153 204 144 82 108 209 117 186 68 96 8 227 113 92 53 131 108 137 148 60 65 162 18 136 204 245 157 252 21 217 254 117 33 178 50 7 95 182 224 243 4 53 ]
c[][] &= [ 248 72 148 136 246 28 65 115 112 19 80 10 201 170 93 35 237 228 203 127 65 167 246 189 123 151 13 226 8 167 215 222 1 121 154 114 172 68 108 3 227 120 21 46 130 63 205 158 57 14 191 18 201 202 238 216 168 23 156 238 116 33 182 60 9 22 131 162 186 37 63 93 ]
c[][] &= [ 248 72 148 139 245 23 3 54 8 86 116 67 201 189 91 50 237 248 202 105 19 181 232 166 101 216 55 190 93 138 218 222 28 107 209 97 186 22 120 74 252 121 29 51 152 108 140 147 48 91 178 87 220 208 248 139 185 88 200 229 111 46 178 36 7 95 182 224 243 4 53 ]
c[][] &= [ 248 72 148 136 246 28 65 115 112 19 94 10 154 255 103 102 166 239 202 107 29 254 196 188 102 216 23 190 93 157 215 222 24 125 223 55 139 12 104 25 175 96 21 44 137 108 131 158 43 70 175 25 207 152 234 145 176 20 156 234 105 96 162 37 70 17 144 162 186 37 63 93 ]
c[][] &= [ 248 72 148 139 245 23 12 54 6 86 107 3 197 255 89 46 172 245 133 120 92 254 255 166 103 216 16 173 81 206 212 155 85 117 148 114 171 68 96 30 175 96 20 36 204 33 140 157 51 14 178 24 197 215 239 138 179 15 156 224 111 36 177 54 80 64 215 205 246 14 51 90 ]
c[][] &= [ 248 72 148 136 246 28 65 115 127 19 64 0 134 187 14 47 169 228 196 50 19 137 227 165 126 216 23 164 77 128 131 141 16 125 209 110 176 17 33 30 224 121 19 51 158 35 154 223 127 102 167 1 205 152 252 216 178 17 223 232 38 37 163 50 71 22 153 235 180 71 18 80 150 ]
#  
proc str2bytes s$ . ar[] .
   ar[] = [ ]
   for c$ in strchars s$
      ar[] &= strcode c$
   .
.
proc bytes2str ar[] . s$ .
   s$ = ""
   for b in ar[]
      s$ &= strchar b
   .
.
proc decrypt encr[] key[] . .
   out[] = [ ]
   for i = 1 to len key[]
      if key[i] <> -1
         out[] &= bitxor encr[i] key[i]
      else
         out[] &= strcode "_"
      .
   .
   bytes2str out[] s$
   print s$
   print ""
.
print ""
txt$ = "______ __ We are going to have a surprise birthday party for Trudy."
str2bytes txt$ b[]
key[] = [ ]
for i = 1 to len b[]
   if b[i] = strcode "_"
      key[] &= -1
   else
      key[] &= bitxor b[i] c[1][i]
   .
.
for i = 1 to len c[][]
   decrypt c[i][] key[]
.

+ Trudy did not crack the key, but only found the bit sequence that was used multiple times to XOR the data. This reuse compromised the security completely.

+ This attack also works with other stream ciphers if you use the same 
key, or the same (key,counter) pair, for encrypting different inputs. 
`

function hook() {
	var dom = window.location.host
	tut.insertAdjacentHTML("beforeend", `
<p><hr><p>
The examples were created with <a href=..>${dom}<a>
<p><small>christof.kaser@gmail.com</small>
`)
}
</script>


