<!doctype html>
<html lang=en>
<meta charset=utf-8><title>Symmetric Cipher</title>
<meta name="viewport" content="width=device-width,initial-scale=1.0">
<meta name="description" content="symmetric encryption, speck, block modes, brute-force, nonce reuse">
<link rel="icon" href="../icon.png" type="image/x-png">

<div id=tut><b>Loading ...</b></div>
<script>

txt_tutor=`* Symmetric Cipher

+ Symmetric ciphers use the same key (generally a very large number)
to encrypt and decrypt the data. This key must be kept secret.

* XOR Encryption

+ XOR encryption with a repeating key is simple, but not secure.

proc str2arr s$ &ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1 : ar[] &= 0
.
proc arr2str ar[] &s$ .
   s$ = ""
   for n in ar[] : for i = 1 to 4
      s$ &= strchar (n mod 256)
      n = n div 256
   .
.
proc xorcrypt key &in[] &out[] .
   out[] = [ ]
   for i = 1 to len in[]
      out[] &= bitxor in[i] key
   .
.
msg$ = "Hi Alice. We have a surprise birthday party for Trudy. Bob"
str2arr msg$ msg[]
#
secrkey = 18238004
#
xorcrypt secrkey msg[] encr[]
print encr[]
print ""
xorcrypt secrkey encr[] r[]
arr2str r[] s$
print s$

* Cracking XOR

+ If data is repeatedly encrypted with the same
XOR key, the encryption can be easily cracked:
The partial keys, e.g. always 8 bits, that encrypt
a character are varied independently until they
"fit" the data they decrypt - a text, for example,
contains many letters and spaces.

proc arr2str ar[] &s$ .
   s$ = ""
   for n in ar[] : for i = 1 to 4
      s$ &= strchar (n mod 256)
      n = n div 256
   .
.
proc xorcrypt key &in[] &out[] .
   out[] = [ ]
   for i = 1 to len in[]
      out[] &= bitxor in[i] key
   .
.
proc crack_xor in[] .
   len k[] 4
   for ki = 1 to 4 : for b = 0 to 255
      k[ki] = b
      key = k[1] + 0x100 * k[2] + 0x10000 * k[3] + 0x1000000 * k[4]
      xorcrypt key in[] out[]
      m = 0
      for i = 1 to len out[] - 1
         h = (bitshift out[i] ((ki - 1) * -8)) mod 256
         if h >= 97 and h <= 122 or h >= 65 and h <= 90 or h = 32 or h = 46
            m += 1
         elif h > 127 or h < 31
            m -= 20
         .
      .
      if m >= len out[] * 4 div 5
         print "Partial key " & ki & " : " & b
         xorcrypt key in[] out[]
         arr2str out[] s$
         print s$
         print ""
         break 1
      .
   .
   print "Key: " & key
.
encr[] = [ 3493208956 4101317464 4097927706 3883344404 4251347537 4101514325 2977393232 3781443399 4100268870 3816761364 4034011712 4033243725 2976857670 2976130386 4116920416 4183976525 4063639901 4286523227 4031081043 4116987712 4097192789 4168625986 3208129882 4084795412 ]
crack_xor encr[]

* Block Cipher

+ *AES* is the most commonly used block cipher. The block size is 128 bits and the key size is usually 128 bits. It is somewhat complicated for micro-controllers and for teaching purposes.

+ The rather unknown *Speck* is well suited for these purposes. This is also a block cipher with different block sizes and key lengths. We use it here for demo purposes with a 64-bit block size, with a 64-bit key length (which is not specified for this block size) and 26 rounds. (The key length is reduced to 53 bits by using a double variable).

+ Blocks of 8 bytes are encrypted together. If two blocks have the same input, they give the same output. This is called *Electronic Codebook (ECB)* mode. To prevent this, the output of one cipher can be combined with the input of the next cipher (*Cipher Block Chaining*).

+ Encryption is done by the basic operations rotation, XOR and addition with the key as parameter in several rounds. For decryption, these steps must be performed in reverse order.

proc str2arr s$ &ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1 : ar[] &= 0
.
proc arr2str ar[] &s$ .
   s$ = ""
   for n in ar[] : for i = 1 to 4
      s$ &= strchar (n mod 256)
      n = n div 256
   .
.
#
proc rot k &x &y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitand bitxor x k 0xffffffff
   y = bitor bitshift y 3 bitshift y -29
   y = bitand bitxor y x 0xffffffff
.
global spkb[] .
proc sp_prepkey0 a b .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc rotrev k &x &y .
   y = bitxor y x
   y = bitand bitor bitshift y -3 bitshift y 29 0xffffffff
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitand bitor bitshift x 8 bitshift x -24 0xffffffff
.
#
proc sp_encr key &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      for j = 1 to 26 : rot spkb[j] x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      for j = 26 downto 1 : rotrev spkb[j] x y
      out[] &= x
      out[] &= y
   .
.
msg$ = "Hi Alice. We have a surprise birthday party for Trudy. Bob"
str2arr msg$ msg[]
#
secrkey = 18238004
#
sp_encr secrkey msg[] encr[]
print encr[]
print ""
sp_decr secrkey encr[] r[]
arr2str r[] s$
print s$
#
#
print ""
print "ECB demo:"
msg$ = "Hi Bob! Hi Joe! Hi Bob! "
str2arr msg$ msg[]
sp_encr 1234567890 msg[] encr[]
print encr[]

* Cipher Block Chaining

+ Before a block is encrypted, it is XORed with the last encrypted block. The
initialization vector *IV* is used for the first block. After decrypting a
block, the same XOR operation must be applied.

+ Equal input blocks do not lead to equal output blocks. But if the blocks at
the beginning are equal, the encryption in these blocks is also equal.
This can be avoided by using a random IV to which the first block is
XORed. This IV must be transmitted (or stored) together with the ciphertext.
Conveniently, it is added as the first block.

proc str2arr s$ &ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1 : ar[] &= 0
.
proc arr2str ar[] &s$ .
   s$ = ""
   for n in ar[] : for i = 1 to 4
      s$ &= strchar (n mod 256)
      n = n div 256
   .
.
#
proc rot k &x &y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitand bitxor x k 0xffffffff
   y = bitor bitshift y 3 bitshift y -29
   y = bitand bitxor y x 0xffffffff
.
global spkb[] .
proc sp_prepkey key .
   a = key div 0x100000000
   b = key mod 0x100000000
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc rotrev k &x &y .
   y = bitxor y x
   y = bitand bitor bitshift y -3 bitshift y 29 0xffffffff
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitand bitor bitshift x 8 bitshift x -24 0xffffffff
.
#
proc sp_encr key &in[] &out[] .
   sp_prepkey key
   x = random 0x100000000
   y = random 0x100000000
   out[] = [ x y ]
   for i = 1 step 2 to len in[]
      x = bitxor in[i] x
      y = bitxor in[i + 1] y
      for j = 1 to 26 : rot spkb[j] x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 3 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      for j = 26 downto 1 : rotrev spkb[j] x y
      out[] &= bitxor x in[i - 2]
      out[] &= bitxor y in[i - 1]
   .
.
msg$ = "Hi Alice. We have planned a surprise birthday party for Trudy this coming Saturday evening. Bob"
str2arr msg$ msg[]
#
secrkey = 18238004
#
sp_encr secrkey msg[] encr[]
print encr[]
print ""
sp_decr secrkey encr[] r[]
arr2str r[] s$
print s$
#
print ""
print "CBC demo:"
msg$ = "Hi Bob! Hi Joe! Hi Bob! "
str2arr msg$ msg[]
sp_encr 1234567890 msg[] encr[]
print encr[]

* Brute Force

+ Every encryption can be cracked by trying all keys. With a key length of 53 bits, that is about 9000 trillion attempts, which is doable with some effort. With 128 bit key length, it would take many billions of years even with the fastest parallel hardware.

+ For a brute force attack, you don't need to know the result you are looking for. The key is the correct one if the decrypted data matches the expected format. In this example, if the decrypted data is all ASCII characters.

+ To shorten the search time in this example, we start the key search near the key.

proc arr2str ar[] &s$ .
   s$ = ""
   for n in ar[] : for i = 1 to 4
      s$ &= strchar (n mod 256)
      n = n div 256
   .
.
#
proc rot k &x &y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitand bitxor x k 0xffffffff
   y = bitor bitshift y 3 bitshift y -29
   y = bitand bitxor y x 0xffffffff
.
global spkb[] .
proc sp_prepkey key .
   a = key div 0x100000000
   b = key mod 0x100000000
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc rotrev k &x &y .
   y = bitxor y x
   y = bitand bitor bitshift y -3 bitshift y 29 0xffffffff
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitand bitor bitshift x 8 bitshift x -24 0xffffffff
.
proc sp_decr key &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 3 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      for j = 26 downto 1 : rotrev spkb[j] x y
      out[] &= bitxor x in[i - 2]
      out[] &= bitxor y in[i - 1]
   .
.
func istext out[] .
   for n in out[] : for j = 1 to 4
      b = n mod 256
      n = n div 256
      if b > 1 and b < 32 or b > 126: return 0
   .
   return 1
.
proc brute_force start in[] &key .
   print "Brute forcing"
   in0[] = [ in[1] in[2] in[3] in[4] ]
   for key = start to 9007199254740991
      if key mod 50000 = 0
         if key mod 1000000 = 0
            print ""
            write key & ": "
         .
         write "."
      .
      sp_decr key in0[] out[]
      if istext out[] = 1
         sp_decr key in[] out[]
         if istext out[] = 1
            print ""
            return
         .
      .
   .
.
encr[] = [ 2995282530 3231593157 2262435520 1455743690 2239111503 754144163 1776451451 2619565752 542475955 2326252767 4024366929 1202124251 2876779313 2374443044 3390291199 3315251173 2771012052 2469472958 3231745358 948080008 3092188112 2968078269 521194386 1697615974 659159096 2703243526 ]
#
brute_force 17000000 encr[] key
print "key: " & key
sp_decr key encr[] r[]
arr2str r[] s$
print s$

+ Who can crack this secret message?

~ [ 1576593714 3827464596 2280490437 3161296180 4078846994 3831482868 1030420123 3080413550 3292128070 1083128452 33181364 296910390 1665490072 3003556078 ]

* Key generation

+ We can use our symmetric cipher to generate a 64-bit output from an input of any length. To do this, we encrypt the output one by one using each 64-bit part of the input as the key, starting at 0. This is a cryptographic hash.

+ We use this to generate a 53-bit key from a variable-length text key. The data is then encrypted with our easy-to-use block cipher in CBC mode - which doesn't go wrong as easily as stream ciphers.

proc str2arr s$ &ar[] .
   # array 32 bit - even length padding
   ar[] = [ ]
   i = 0x100000000
   for c$ in strchars s$
      if i = 0x100000000
         ar[] &= 0
         i = 1
      .
      ar[len ar[]] += strcode c$ * i
      i *= 256
   .
   if len ar[] mod 2 = 1 : ar[] &= 0
.
proc arr2str ar[] &s$ .
   s$ = ""
   for n in ar[] : for i = 1 to 4
      s$ &= strchar (n mod 256)
      n = n div 256
   .
.
proc rot k &x &y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitand bitxor x k 0xffffffff
   y = bitor bitshift y 3 bitshift y -29
   y = bitand bitxor y x 0xffffffff
.
global spkb[] .
proc sp_prepkey0 a b .
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_prepkey key .
   a = key div 0x100000000
   b = key mod 0x100000000
   sp_prepkey0 a b
.
proc rotrev k &x &y .
   y = bitxor y x
   y = bitand bitor bitshift y -3 bitshift y 29 0xffffffff
   x = bitxor x k
   x = (x - y) mod 0x100000000
   x = bitand bitor bitshift x 8 bitshift x -24 0xffffffff
.
proc sp_encr key &in[] &out[] .
   sp_prepkey key
   x = random 0x100000000
   y = random 0x100000000
   out[] = [ x y ]
   for i = 1 step 2 to len in[]
      x = bitxor in[i] x
      y = bitxor in[i + 1] y
      for j = 1 to 26 : rot spkb[j] x y
      out[] &= x
      out[] &= y
   .
.
proc sp_decr key &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 3 step 2 to len in[]
      x = in[i]
      y = in[i + 1]
      for j = 26 downto 1 : rotrev spkb[j] x y
      out[] &= bitxor x in[i - 2]
      out[] &= bitxor y in[i - 1]
   .
.
func genkey s$ .
   str2arr s$ b[]
   for i = 1 step 2 to len b[] - 1
      xp = x
      yp = y
      sp_prepkey0 b[i] b[i + 1]
      for j = 1 to 26 : rot spkb[j] x y
      x = bitxor x xp
      y = bitxor y yp
   .
   # make a 53 bit value for the "double" variable
   return x + y mod 2097152 * 0x100000000
.
#
msg$ = "Hello Alice. We're planning a surprise party for Trudy next weekend. Kind regards, Bob"
str2arr msg$ msg[]
sp_encr genkey "That is a very long password!!" msg[] encr[]
print encr[]
print ""
sp_decr genkey "That is a very long password!!" encr[] r[]
arr2str r[] s$
print s$

* Counter Mode

+ If you encrypt a continuous counter with a block cipher, you
get a bit sequence that cannot be predicted without knowing the
key. With this sequence then the input data is XORed and you
get a stream cipher.

+ The counter must not occur more than once for the same key,
otherwise the encryption is vulnerable. The counter does not
have to be kept secret. It can be transmitted together with
the encrypted data.

+ The start counter is also called *Nonce* ("Number Once"). The
actual counter can also be combined with a start value in a
different way (XOR, concatenate).

proc bytes2str ar[] &s$ .
   s$ = ""
   for b in ar[] : s$ &= strchar b
.
proc str2bytes s$ &b[] .
   b[] = [ ]
   for c$ in strchars s$ : b[] &= strcode c$
.
proc rot k &x &y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitand bitxor x k 0xffffffff
   y = bitor bitshift y 3 bitshift y -29
   y = bitand bitxor y x 0xffffffff
.
global spkb[] .
proc sp_prepkey key .
   a = key div 0x100000000
   b = key mod 0x100000000
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_ctrmode key ctr &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 to len in[]
      m = (i - 1) mod 8
      if m = 0
         dx = ctr mod 0x100000000
         dy = ctr div 0x100000000
         for j = 1 to 26 : rot spkb[j] dx dy
         ctr += 1
      .
      if m <= 3
         h = (bitshift dx (-8 * m)) mod 255
      else
         m -= 4
         h = (bitshift dy (-8 * m)) mod 255
      .
      out[] &= bitxor in[i] h
   .
.
msg$ = "Hi Alice. We have a surprise birthday party for Trudy. Bob"
str2bytes msg$ msg[]
#
secrkey = 18238004
counter = 1423
#
sp_ctrmode secrkey counter msg[] encr[]
print encr[]
print ""
sp_ctrmode secrkey counter encr[] r[]
bytes2str r[] s$
print s$

* The counter is important in counter mode.

+ Bob and Alice exchange secret messages. A fatal mistake
happens to them doing so.

proc str2bytes s$ &b[] .
   b[] = [ ]
   for c$ in strchars s$ : b[] &= strcode c$
.
proc rot k &x &y .
   x = bitor bitshift x -8 bitshift x 24
   x += y
   x = bitand bitxor x k 0xffffffff
   y = bitor bitshift y 3 bitshift y -29
   y = bitand bitxor y x 0xffffffff
.
global spkb[] .
proc sp_prepkey key .
   a = key div 0x100000000
   b = key mod 0x100000000
   spkb[] = [ b ]
   for i = 0 to 24
      rot i a b
      spkb[] &= b
   .
.
proc sp_ctrmode key ctr &in[] &out[] .
   sp_prepkey key
   out[] = [ ]
   for i = 1 to len in[]
      m = (i - 1) mod 8
      if m = 0
         dx = ctr mod 0x100000000
         dy = ctr div 0x100000000
         for j = 1 to 26 : rot spkb[j] dx dy
         ctr += 1
      .
      if m <= 3
         h = (bitshift dx (-8 * m)) mod 255
      else
         m -= 4
         h = (bitshift dy (-8 * m)) mod 255
      .
      out[] &= bitxor in[i] h
   .
.
#
secrkey = 418785432574
counter = 0
#
msg$[] &= "Hi Alice. We are going to have a surprise birthday party for Trudy. Bob"
msg$[] &= "Hi Bob! Great. Hopefully she won't be able to decode the message. Alice"
msg$[] &= "Hi Alice! We use encryption. It takes millions of years to crack it. Bob"
msg$[] &= "Hi Bob! Yes, but you know Trudy is very smart about these things. Alice"
msg$[] &= "Hi Alice! Yes I know. But trust me. This time nothing will go wrong. Bob"
msg$[] &= "Hi Bob. Well, what do you say we meet at the mall tomorrow midday? Alice"
msg$[] &= "Hi Alice. Good idea. Well then see you tomorrow. Have a nice evening. Bob"
#
for i = 1 to len msg$[]
   str2bytes msg$[i] msg[]
   sp_ctrmode secrkey counter msg[] encr[]
   print encr[]
   print ""
.

+ Trudy can intercept the encrypted messages and tests if she can
find a common bit sequence for the different messages that decrypt
the messages. She uses a letter frequency analysis for this.

c[][] &= [ 234 165 225 65 201 168 255 208 125 54 231 57 115 199 197 116 154 223 200 223 17 16 99 33 57 157 212 100 173 51 235 233 12 85 227 143 192 17 130 37 17 184 207 172 40 195 65 213 169 106 215 76 126 219 74 192 39 201 28 242 251 242 199 214 44 160 47 29 201 71 80 ]
c[][] &= [ 234 165 225 66 202 163 189 149 20 100 213 61 39 136 151 89 213 200 194 208 10 27 47 44 118 206 212 96 251 33 164 230 11 82 182 159 213 67 138 52 24 253 141 177 53 151 77 212 171 124 147 89 63 221 86 220 39 194 22 243 168 199 210 198 102 249 64 81 226 75 87 ]
c[][] &= [ 234 165 225 65 201 168 255 208 114 54 231 57 115 211 196 116 154 221 201 213 13 14 51 33 63 210 210 43 251 31 191 168 88 71 253 152 195 67 134 63 24 244 196 170 52 196 9 222 174 51 142 89 126 219 77 153 115 192 83 227 169 199 214 200 104 176 117 19 171 106 93 244 ]
c[][] &= [ 234 165 225 66 202 163 189 149 10 115 195 112 115 196 194 101 154 193 200 195 95 28 45 58 33 157 232 119 174 50 178 168 69 85 182 139 213 17 146 118 7 245 204 183 46 151 72 211 167 102 131 28 107 193 91 202 98 143 7 232 178 200 210 208 102 249 64 81 226 75 87 ]
c[][] &= [ 234 165 225 65 201 168 255 208 114 54 233 57 32 134 254 49 209 214 200 193 81 87 1 32 34 157 200 119 174 37 191 168 65 67 184 221 228 11 130 37 84 236 196 168 63 151 71 222 188 123 158 82 120 137 73 208 107 195 83 231 180 134 194 209 39 183 102 19 171 106 93 244 ]
c[][] &= [ 234 165 225 66 202 163 178 149 4 115 220 48 127 134 192 121 219 204 135 210 16 87 58 58 35 157 207 100 162 118 188 237 12 75 243 152 196 67 138 34 84 236 197 160 122 218 72 221 164 51 131 83 114 198 76 203 104 216 83 237 178 194 209 194 49 230 33 124 231 65 81 243 ]
c[][] &= [ 234 165 225 65 201 168 255 208 125 54 247 51 60 194 151 120 222 221 198 152 95 32 38 57 58 157 200 109 190 56 235 251 73 67 182 132 223 22 203 34 27 245 194 183 40 216 94 159 232 91 150 74 122 137 95 153 105 198 16 229 251 195 195 198 38 176 111 90 165 8 112 249 131 ]
#
proc bytes2str ar[] &s$ .
   s$ = ""
   for b in ar[] : s$ &= strchar b
.
proc crack_xor &key[] .
   key[] = [ ]
   for pos = 1 to 66
      max = 1
      maxcnt = 0
      for b = 0 to 0xff
         out[] = [ ]
         for i = 1 to len c[][]
            out[] &= bitxor c[i][pos] b
         .
         m = 0
         for o in out[]
            o$ = strchar o
            if o >= 65 and o <= 90
               m += 3
            elif o >= 97 and o <= 122
               m += 6
            elif o < 32
               m -= 5
            .
            if o$ = "e"
               m += 3
            elif o$ = "a" or o$ = "t"
               m += 2
            elif o$ = "n" or o$ = "o" or o$ = "r" or o$ = "s" or o$ = "i" or o$ = "h" or o$ = "i"
               m += 1
            elif o$ = " "
               m += 9
            elif o$ = "."
               m += 6
            elif o$ = "k" or o$ = "v" or o$ = "b"
               m -= 1
            elif o$ = "z" or o$ = "x" or o$ = "q" or o$ = "j"
               m -= 2
            .
         .
         if m >= max
            maxcnt += 1
            if m > max
               maxcnt = 1
               max = m
               maxb = b
            .
         .
      .
      if maxcnt = 1
         key[] &= maxb
      else
         key[] &= -1
      .
   .
.
crack_xor key[]
#
proc decrypt encr[] key[] .
   out[] = [ ]
   for i = 1 to len key[]
      if key[i] <> -1
         out[] &= bitxor encr[i] key[i]
      else
         out[] &= strcode "_"
      .
   .
   bytes2str out[] s$
   print s$
   print ""
.
for i = 1 to len c[][] : decrypt c[i][] key[]

+ That worked. Alice and Bob obviously messed up and generated the
same XOR bit sequence in a stream cipher for all messages - they
didn't know what the counter was for, so they just set it to 0.

+ The first and last characters of the messages could not be
decoded, because they differed too little between the messages.

+ Trudy can complete the first message even further without a
computer and thus also determine the XOR key sequence and thus
also decode the further messages almost completely.

c[][] &= [ 234 165 225 65 201 168 255 208 125 54 231 57 115 199 197 116 154 223 200 223 17 16 99 33 57 157 212 100 173 51 235 233 12 85 227 143 192 17 130 37 17 184 207 172 40 195 65 213 169 106 215 76 126 219 74 192 39 201 28 242 251 242 199 214 44 160 47 29 201 71 80 ]
c[][] &= [ 234 165 225 66 202 163 189 149 20 100 213 61 39 136 151 89 213 200 194 208 10 27 47 44 118 206 212 96 251 33 164 230 11 82 182 159 213 67 138 52 24 253 141 177 53 151 77 212 171 124 147 89 63 221 86 220 39 194 22 243 168 199 210 198 102 249 64 81 226 75 87 ]
c[][] &= [ 234 165 225 65 201 168 255 208 114 54 231 57 115 211 196 116 154 221 201 213 13 14 51 33 63 210 210 43 251 31 191 168 88 71 253 152 195 67 134 63 24 244 196 170 52 196 9 222 174 51 142 89 126 219 77 153 115 192 83 227 169 199 214 200 104 176 117 19 171 106 93 244 ]
c[][] &= [ 234 165 225 66 202 163 189 149 10 115 195 112 115 196 194 101 154 193 200 195 95 28 45 58 33 157 232 119 174 50 178 168 69 85 182 139 213 17 146 118 7 245 204 183 46 151 72 211 167 102 131 28 107 193 91 202 98 143 7 232 178 200 210 208 102 249 64 81 226 75 87 ]
c[][] &= [ 234 165 225 65 201 168 255 208 114 54 233 57 32 134 254 49 209 214 200 193 81 87 1 32 34 157 200 119 174 37 191 168 65 67 184 221 228 11 130 37 84 236 196 168 63 151 71 222 188 123 158 82 120 137 73 208 107 195 83 231 180 134 194 209 39 183 102 19 171 106 93 244 ]
c[][] &= [ 234 165 225 66 202 163 178 149 4 115 220 48 127 134 192 121 219 204 135 210 16 87 58 58 35 157 207 100 162 118 188 237 12 75 243 152 196 67 138 34 84 236 197 160 122 218 72 221 164 51 131 83 114 198 76 203 104 216 83 237 178 194 209 194 49 230 33 124 231 65 81 243 ]
c[][] &= [ 234 165 225 65 201 168 255 208 125 54 247 51 60 194 151 120 222 221 198 152 95 32 38 57 58 157 200 109 190 56 235 251 73 67 182 132 223 22 203 34 27 245 194 183 40 216 94 159 232 91 150 74 122 137 95 153 105 198 16 229 251 195 195 198 38 176 111 90 165 8 112 249 131 ]
#
proc str2bytes s$ &ar[] .
   ar[] = [ ]
   for c$ in strchars s$
      ar[] &= strcode c$
   .
.
proc bytes2str ar[] &s$ .
   s$ = ""
   for b in ar[] : s$ &= strchar b
.
proc decrypt encr[] key[] .
   out[] = [ ]
   for i = 1 to len key[]
      if key[i] <> -1
         out[] &= bitxor encr[i] key[i]
      else
         out[] &= strcode "_"
      .
   .
   bytes2str out[] s$
   print s$
   print ""
.
print ""
txt$ = "______ __ We are going to have a surprise birthday party for Trudy."
str2bytes txt$ b[]
key[] = [ ]
for i = 1 to len b[]
   if b[i] = strcode "_"
      key[] &= -1
   else
      key[] &= bitxor b[i] c[1][i]
   .
.
for i = 1 to len c[][] : decrypt c[i][] key[]

+ Trudy did not crack the key, but only found the bit sequence that was used multiple times to XOR the data. This reuse compromised the security completely.

+ This attack also works with other stream ciphers if you use the same
key, or the same (key,counter) pair, for encrypting different inputs.
`

function hook() {
	var dom = window.location.host
	tut.insertAdjacentHTML("beforeend", `
<p><hr><p>
The examples were created with <a href=..>${dom}</a>
<p><small>christof.kaser@gmail.com</small>
`)
}
</script>
<script src=easy_code2.js></script>
<script src=easy_tut2.js></script>
