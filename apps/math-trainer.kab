# Math trainer
# 
ind = -1
q$ = ""
subr init_vars
   resok = 0
   res = 0
   t = systime
   state = 1
   op = ind
   if op <= 2
      todo = 10
   elif op <= 4
      todo = 5
   else
      todo = 3
   .
.
func disp col s$ . .
   color col
   move 3 76
   rect 94 21
   color 888
   move 6 82
   textsize 12
   text s$
.
func show_quest . .
   if res = resok
      if op = 1
         a = random 8 + 1
         b = random 8 + 1
         q$ = a & " * " & b & " = "
         resok = a * b
      elif op = 2
         b = random 8 + 1
         resok = random 8 + 1
         a = b * resok
         q$ = a & " / " & b & " = "
      elif op = 3
         a = random 77 + 10
         b = random (89 - a) + 10
         q$ = a & " + " & b & " = "
         resok = a + b
      elif op = 4
         a = random 70 + 29
         b = random (a - 11) + 10
         q$ = a & " - " & b & " = "
         resok = a - b
      elif op = 5
         a = random 89 + 10
         b = random 8 + 1
         q$ = a & " * " & b & " = "
         resok = a * b
      .
   .
   call disp 030 q$ & "_"
   res = 0
.
func draw_key col ind . .
   s$ = strchar (ind + 48)
   h = ind
   if h < 10 and h > 0
      h -= 1
      x = h mod 3 * 24 + 3
      y = 2 - h div 3
   else
      x = 3 * 24 + 3
      if h = 0
         y = 1
      elif h = 10
         y = 2
         s$ = "Ã—"
      else
         y = 0
         s$ = "âœ“"
      .
   .
   y = y * 24 + 3
   color col
   move x y
   rect 22 22
   move x + 5 y + 4.5
   color 888
   textsize 16
   text s$
.
background 010
func start . .
   call init_vars
   clear
   for ind = 0 to 11
      call draw_key 020 ind
   .
   call show_quest
.
menu$[] = [ "Mul ğŸ¤”" "Div ğŸ¤”" "Add ğŸ¤”ğŸ¤”" "Sub ğŸ¤”ğŸ¤”" "Mul ğŸ¤”ğŸ¤”" ]
func draw_menu_key col i . .
   y = (5 - i) * 19 + 3
   move 3 y
   color col
   rect 94 17
   color 888
   textsize 12
   move 16 y + 5
   text substr menu$[i] 1 3
   textsize 7
   move 50 y + 5
   text substr menu$[i] 4 -1
.
func draw_menu . .
   clear
   for i = 1 to 5
      call draw_menu_key 020 i
   .
   state = 0
.
func handle_key2 . .
   if ind = 11
      state = 2
      if res = resok
         col = 050
         todo -= 1
         timer 0.5
      else
         col = 500
         todo += 1
         timer 2
      .
      call disp col q$ & res
   else
      if ind = 10
         res = res div 10
         h$ = res
         if res = 0
            h$ = "_"
         .
      else
         if res >= 100
            res = 0
         .
         res = res * 10 + ind
         h$ = res
      .
      call disp 030 q$ & h$
   .
.
func handle_key . .
   c = mouse_x div 25
   r = 2 - mouse_y div 25
   if c < 3
      ind = r * 3 + c + 1
   else
      if r = 1
         ind = 0
      elif r = 0
         ind = 10
      else
         ind = 11
      .
   .
   call draw_key 040 ind
   call handle_key2
.
on key
   ind = strcode keybkey - 48
   if state = 1
      if ind >= 0 and ind <= 9
         # 
      elif ind = 21
         ind = 11
      elif ind = 18 or ind = 20
         ind = 10
      else
         ind = -1
      .
      if ind >= 0
         call handle_key2
      .
   elif state = 0
      if ind >= 1 and ind <= 4
         ind -= 1
         call start
      .
   elif state = 4
      call draw_menu
   .
.
on mouse_down
   if state = 0
      ind = 5 - mouse_y div 20
      call draw_menu_key 030 ind
   elif state = 1
      if mouse_y < 75
         call handle_key
      elif res = 0
         ind = -1
         call draw_menu
      .
   elif state = 4
      call draw_menu
   .
.
on mouse_up
   if ind >= 0
      if state = 0
         call start
      elif state = 1 or state = 2
         call draw_key 020 ind
      .
      ind = -1
   .
.
func smiley t . .
   if t <= 60
      move 65 30
      color 983
      circle 19
      color 000
      move 58 37
      circle 2
      move 72 37
      circle 2
      linewidth 2
      if t <= 30
         curve [ 56 27 65 14 74 27 ]
      else
         curve [ 58 23 65 17 72 23 ]
      .
   .
.
func show_result . .
   state = 3
   timer 2
   t = floor (systime - t)
   move 3 3
   color 030
   rect 94 94
   color 888
   move 10 84
   textsize 10
   text "Well done"
   move 10 70
   text "Time: " & t
   move 10 56
   textsize 5
   text menu$[op]
   call smiley t
.
on timer
   if state = 3
      state = 4
   elif state = 2 and todo = 0
      call show_result
   elif state = 2
      call show_quest
      state = 1
   .
.
call draw_menu