# Mandelbrot
#  
center_x = 300
center_y = 200
scale = 200
background 000
textsize 2
# 
fastproc iter cx cy . iter .
   iter = 0
   repeat
      y = 2 * x * y + cy
      x = xx - yy + cx
      iter += 1
      xx = x * x
      yy = y * y
      until xx + yy > 4 or iter = 128
   .
.
proc draw . .
   clear
   for scr_y = 0 to 399
      cy = (scr_y - center_y) / scale
      for scr_x = 0 to 399
         cx = (scr_x - center_x) / scale
         call iter cx cy iter
         if iter < 128
            color3 iter / 32 iter / 128 0
            # color3 0 iter / 32 iter / 128
            move scr_x / 4 scr_y / 4
            rect 0.25 0.25
         .
      .
   .
   color 990
   move 1 1
   text "Short press to zoom in, long to zoom out"
.
on mouse_down
   time0 = systime
.
on mouse_up
   center_x += 200 - mouse_x * 4
   center_y += 200 - mouse_y * 4
   if systime - time0 < 0.3
      center_x -= 200 - center_x
      center_y -= 200 - center_y
      scale *= 2
   else
      center_x += (200 - center_x) * 3 / 4
      center_y += (200 - center_y) * 3 / 4
      scale /= 4
   .
   call draw
.
call draw