# Letter memory
#
subr square
   h = ind - 1
   x = h mod 4 * 24 + 6
   y = h div 4 * 24 + 6
   linewidth 6
   move x y
   line x + 16 y
   line x + 16 y + 16
   line x y + 16
   line x y
   rect 16 16
.
subr close_card
   open[ind] = 0
   color 131
   square
.
subr open_card
   open[ind] = 1
   color 464
   square
   move x + 3 y + 3
   color 000
   text cards$[cards[ind]]
.
background 000
subr start
   clear
   for ind to 16
      close_card
   .
   # shuffle
   for i = 16 downto 2
      r = randint i
      swap cards[r] cards[i - 1]
   .
   card1 = -1
   card2 = -1
   color 000
   textsize 16
   opencards = 0
   starttime = systime
.
subr search_card
   c = mouse_x div 25
   r = mouse_y div 25
   ind = r * 4 + c + 1
   if card1 <> -1 and card2 <> -1 or open[ind] = 1
      # sound [ 440 0.15 ]
   else
      open_card
      if card1 = -1
         card1 = ind
      elif (cards[card1] + 1) div 2 <> (cards[ind] + 1) div 2
         card2 = ind
         timer 1
      else
         card1 = -1
         opencards += 2
         if opencards = 16
            timer 1
         .
      .
   .
.
on mouse_down
   if opencards = -1
      start
   elif opencards < 16
      search_card
   .
.
proc smiley t . .
   if t <= 60
      move 65 30
      color 983
      circle 19
      color 000
      move 58 37
      circle 2
      move 72 37
      circle 2
      linewidth 2
      if t <= 30
         curve [ 56 27 65 14 74 27 ]
      else
         curve [ 58 23 65 17 72 23 ]
      .
   .
.
on timer
   if opencards = 16
      opencards = -1
      clear
      textsize 10
      color 797
      move 10 80
      text "Well done!"
      move 10 65
      t = floor (systime - starttime - 0.5)
      text "Time: " & t
      smiley t
   else
      ind = card1
      close_card
      ind = card2
      close_card
      card1 = -1
      card2 = -1
   .
.
len cards[] 16
for i to 16
   cards[i] = i
.
cards$[] = strchars "AABBDDEEFFGGHHII"
len open[] 16
start