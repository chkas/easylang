# 1x1 trainer
#
len n[] 10
len m[] 10
subr init_vars
   n = 0
   m = 0
   state = 0
   sz = 10
   ind = -1
   t = 0
   row = -1
   res = 0
.
proc disp col s$ . .
   color col
   move 3 76
   rect 94 21
   color 888
   move 7 82
   textsize 12
   text s$
.
proc show_quest . .
   j = randint sz
   if row < 10
      n = n[j]
      n[j] = n[sz]
      n[sz] = n
      m = row
   else
      n = n[sz]
      m = m[sz]
   .
   sz -= 1
   disp 030 n & " × " & m & " = _"
   res = 0
.
proc sel_row . .
   if ind <= 10
      t = systime
      row = ind
      if row = 0
         row = 11
      .
      if row < 10
         for i = 1 to 10
            n[i] = i
         .
      else
         for i = 1 to 10
            n[i] = randint 8 + 1
            m[i] = randint 8 + 1
         .
      .
      sz = 10
      show_quest
   .
.
proc draw_key . .
   s$ = strchar (ind + 48)
   h = ind
   if h < 10 and h > 0
      h -= 1
      x = h mod 3 * 24 + 3
      y = 2 - h div 3
   else
      x = 3 * 24 + 3
      if h = 0
         y = 1
      elif h = 10
         y = 2
         s$ = "⌫"
      else
         y = 0
         s$ = "✓"
      .
   .
   y = y * 24 + 3
   move x y
   rect 22 22
   move x + 5 y + 4.5
   color 888
   textsize 16
   text s$
.
background 010
proc init . .
   init_vars
   clear
   for ind = 0 to 11
      color 020
      draw_key
   .
   ind = -1
   disp 030 "Which row?"
.
proc sel_inp . .
   if ind = 11
      state = 1
      if n * m = res
         col = 050
         timer 0.5
      else
         sz += 1
         col = 500
         timer 3
      .
      h$ = n * m
   else
      if ind = 10
         res = res div 10
         h$ = res
         if res = 0
            h$ = "_"
         .
      else
         if res >= 10
            res = 0
         .
         res = res * 10 + ind
         h$ = res
      .
      col = 030
   .
   disp col n & " × " & m & " = " & h$
.
proc handle_key2 . .
   if row = -1
      sel_row
   else
      sel_inp
   .
.
proc handle_key . .
   c = mouse_x div 25
   r = 2 - mouse_y div 25
   if c < 3
      ind = r * 3 + c + 1
   else
      if r = 1
         ind = 0
      elif r = 0
         ind = 10
      else
         ind = 11
      .
   .
   color 040
   draw_key
   handle_key2
.
on key
   ind = strcode keybkey - 48
   if state = 0
      if ind >= 0 and ind <= 9
         #
      elif ind = 21
         ind = 11
      elif ind = 18 or ind = 20
         ind = 10
      else
         ind = -1
      .
      if ind >= 0
         handle_key2
      .
   elif state = 3
      init
   .
.
on mouse_down
   if state = 3
      init
   elif state = 0
      if mouse_y < 75
         handle_key
      elif res = 0
         init
      .
   .
.
on mouse_up
   if ind >= 0
      color 020
      draw_key
   .
.
proc smiley t . .
   if t <= 60
      move 65 30
      color 983
      circle 19
      color 000
      move 58 37
      circle 2
      move 72 37
      circle 2
      linewidth 2
      if t <= 30
         curve [ 56 27 65 14 74 27 ]
      else
         curve [ 58 23 65 17 72 23 ]
      .
   .
.
proc done . .
   t = floor (systime - t)
   move 0 0
   color 030
   rect 100 100
   color 888
   move 10 84
   textsize 10
   text "Well done!"
   move 10 70
   s$ = "All"
   if row < 10
      s$ = row
   .
   text "Row: " & s$
   move 10 56
   text "Time: " & t
   smiley t
.
on timer
   if state = 2
      state = 3
   elif sz = 0
      state = 2
      timer 2
      done
   else
      show_quest
      state = 0
   .
.
init