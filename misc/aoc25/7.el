# AoC-25 - Day 7: Laboratories
#
repeat
   s$ = input
   until s$ = ""
   m$[][] &= strchars s$
.
nc = len m$[1][]
for i to nc : p[] &= if m$[1][i] = "S"
#
for r = 2 to len m$[][]
   pn[] = [ ]
   len pn[] nc
   for p = 1 to nc
      if m$[r][p] = "^"
         if p[p] > 0 : cnt1 += 1
         pn[p - 1] += p[p]
         pn[p + 1] += p[p]
      else
         pn[p] += p[p]
      .
   .
   swap p[] pn[]
.
for p in p[] : cnt2 += p
print cnt1
print cnt2
#
input_data
.......S.......
...............
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............
