func[] intersect a[] b[] .
   dax = a[3] - a[1]
   day = a[4] - a[2]
   dbx = b[3] - b[1]
   dby = b[4] - b[2]
   h1 = a[2] - b[2]
   h2 = dby * (a[1] - b[1])
   d = dby * dax - dbx * day
   ua = (dbx * h1 - h2) / d
   return [ a[1] + ua * dax a[2] + ua * day ]
.
drawgrid
coord_scale 10
glinewidth 0.03
proc drawline l[] .
   gcolor -1
   gcircle l[1] l[2] 0.1
   gcircle l[3] l[4] 0.1
   a[] = intersect l[] [ 0 0 100 0 ]
   if abs a[1] > 100 : a[] = intersect l[] [ 0 0 0 100 ]
   b[] = intersect l[] [ 100 0 100 100 ]
   if abs b[1] > 100 : b[] = intersect l[] [ 0 100 100 100 ]
   gline a[1] a[2] b[1] b[2]
.
proc drawpt p[] .
   gcolor 900
   gcircle p[1] p[2] 0.1
.
line1[] = [ 4 0 6 10 ]
line2[] = [ 0 3 10 7 ]
drawline line1[]
drawline line2[]
pt[] = intersect line1[] line2[]
print pt[]
drawpt pt[]
#
line3[] = [ 5 1 9 2 ]
drawline line3[]
pt[] = intersect line1[] line3[]
print pt[]
drawpt pt[]
