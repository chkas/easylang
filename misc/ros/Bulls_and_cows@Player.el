size = 4
choices$[] = [ ]
proc mkperms pre$ rest$ sz .
   if len pre$ = sz
      choices$[] &= pre$
      return
   .
   for i = 1 to len rest$
      npre$ = pre$ & substr rest$ i 1
      nrest$ = substr rest$ 1 (i - 1) & substr rest$ (i + 1) 99
      mkperms npre$ nrest$ sz
   .
.
mkperms "" "123456789" size
for i = len choices$[] downto 2
   j = random i
   swap choices$[i] choices$[j]
.
answers$[] = [ ]
print "Playing Bulls & Cows with " & size & " unique digits"
while 1 = 1
   ans$ = choices$[1]
   answers$[] &= ans$
   write "Guess " & len answers$[] & " is " & ans$ & ". Enter score (bulls,cows): "
   score$ = input
   print score$
   a$[] = strtok score$ ", "
   bulls = number a$[1]
   cows = number a$[2]
   if bulls = size and cows = 0
      print "Ye-haw!"
      break 1
   .
   nchoices$[] = [ ]
   for c$ in choices$[]
      b = 0
      cw = 0
      for i = 1 to size
         if substr c$ i 1 = substr ans$ i 1
            b += 1
         elif strpos ans$ substr c$ i 1 > 0
            cw += 1
         .
      .
      if b = bulls and cw = cows
         nchoices$[] &= c$
      .
   .
   swap choices$[] nchoices$[]
   if len choices$[] = 0
      print "Bad scoring? Nothing fits!"
      break 1
   .
.
