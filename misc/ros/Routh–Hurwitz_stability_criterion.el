DEF_TOL = 1e-9
EPS_REPL = 0.01
func cbrt x .
   return sign x * pow abs x (1 / 3)
.
proc routh coeff[] .
   deg = len coeff[] - 1
   cols = (deg + 2) div 2
   len tbl[][] deg + 1
   for r = 1 to deg + 1 : len tbl[r][] cols
   for c = 1 to cols
      idx = 2 * c - 1
      if idx <= deg + 1
         tbl[1][c] = coeff[idx]
      else
         tbl[1][c] = 0
      .
   .
   for c = 1 to cols
      idx = 2 * c
      if idx <= deg + 1
         tbl[2][c] = coeff[idx]
      else
         tbl[2][c] = 0
      .
   .
   for r = 3 to deg + 1
      for c = 1 to cols - 1
         ul = tbl[r - 2][1]
         ur = tbl[r - 2][c + 1]
         ml = tbl[r - 1][1]
         mr = tbl[r - 1][c + 1]
         den = ml
         if abs den < DEF_TOL : den = EPS_REPL
         tbl[r][c] = ((ml * ur) - (ul * mr)) / den
      .
      tbl[r][cols] = 0
      if abs tbl[r][1] < DEF_TOL : tbl[r][1] = EPS_REPL
   .
   for r = 1 to deg
      if sign tbl[r][1] <> sign tbl[r + 1][1] : unst += 1
   .
   print ""
   print "Routh-Hurwitz Table:"
   for r = 1 to (deg + 1)
      for c = 1 to cols
         v = tbl[r][c]
         if c = 1 : write v
         if c > 1 and v <> 0 : write " | " & v
      .
      print ""
   .
   if unst = 0
      print "System is STABLE"
   else
      print "System is UNSTABLE with " & unst & " pole(s)"
   .
.
proc roots coeff[] .
   deg = len coeff[] - 1
   if deg <> 3 : return
   a = coeff[2] / coeff[1]
   b = coeff[3] / coeff[1]
   c = coeff[4] / coeff[1]
   Q = (3 * b - a * a) / 9
   R = (9 * a * b - 27 * c - 2 * a * a * a) / 54
   D = Q * Q * Q + R * R
   print "Roots of the polynomial:"
   if D >= 0
      S = cbrt (R + sqrt D)
      T = cbrt (R - sqrt D)
      x1 = -a / 3 + (S + T)
      x2 = -a / 3 - (S + T) / 2
      x3 = -a / 3 - (S + T) / 2
      print x1
      print x2
      print x3
   else
      th = acos (R / sqrt (-Q * Q * Q))
      twos = 2 * sqrt (-Q)
      x1 = twos * cos (th / 3) - a / 3
      x2 = twos * cos ((th + 360) / 3) - a / 3
      x3 = twos * cos ((th - 360) / 3) - a / 3
      print x1
      print x2
      print x3
   .
.
func$ rep ch$ n .
   for i = 1 to n : r$ &= ch$
   return r$
.
numfmt 6 0
print "--- Stable: s^3+6s^2+11s+6 ---"
coeff[] = [ 1 6 11 6 ]
routh coeff[]
roots coeff[]
print rep "-" 50
print "--- Unstable: s^3-6s^2+11s-6 ---"
coeff2[] = [ 1 -6 11 -6 ]
routh coeff2[]
roots coeff2[]
print rep "-" 50
