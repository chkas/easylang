func islower c$ .
   code = strcode c$
   if code >= strcode "a" and code <= strcode "z" : return 1
   return 0
.
func isupper c$ .
   code = strcode c$
   if code >= strcode "A" and code <= strcode "Z" : return 1
   return 0
.
func isletter c$ .
   if isupper c$ = 1 or islower c$ = 1 : return 1
   return 0
.
func$ tolower c$ .
   code = strcode c$
   if code >= strcode "A" and code <= strcode "Z"
      code += strcode "a" - strcode "A"
      return strchar code
   .
   return c$
.
func$ toupper c$ .
   code = strcode c$
   if code >= strcode "a" and code <= strcode "z"
      code += strcode "A" - strcode "a"
      return strchar code
   .
   return c$
.
func isvowel c$ .
   u$ = tolower c$
   if u$ = "a" or u$ = "e" or u$ = "i" or u$ = "o" or u$ = "u" : return 1
   return 0
.
func$ piglatin s$ .
   spos = 1
   smax = len s$
   while spos <= smax
      c$ = substr s$ spos 1
      if isletter c$ = 0
         res$ &= c$
         spos += 1
      else
         front$ = ""
         word$ = ""
         uppercase = isupper c$
         repeat
            c$ = tolower substr s$ spos 1
            until isletter c$ = 0 or isvowel c$ = 1
            front$ &= c$
            if spos < smax
               c2$ = tolower substr s$ (spos + 1) 1
               if c$ = "q" and c2$ = "u"
                  front$ &= "u"
                  spos += 1
               .
            .
            spos += 1
         .
         repeat
            c$ = tolower substr s$ spos 1
            until isletter c$ = 0
            word$ &= c$
            spos += 1
         .
         if front$ = ""
            word$ &= "way"
         else
            word$ &= front$ & "ay"
         .
         if uppercase = 1
            word$ = toupper substr word$ 1 1 & substr word$ 2 (len word$ - 1)
         .
         res$ &= word$
      .
   .
   return res$
.
print piglatin "Hello, World!"
print piglatin "Apple pie."
print piglatin "Queen and quick."
