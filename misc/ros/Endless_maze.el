global x[] y[] e[] pos dir offx offy .
# 
dx[] = [ 1 0 -1 0 ]
dy[] = [ 0 1 0 -1 ]
# 
proc draw .
   if x[pos] + offx < 0 : offx += 1
   if x[pos] + offx >= 25 : offx -= 1
   if y[pos] + offy < 0 : offy += 1
   if y[pos] + offy >= 25 : offy -= 1
   gclear
   gcolor 888
   for i to len x[]
      x = 4 * x[i] + 0.25 + offx * 4
      y = 4 * y[i] + 0.25 + offy * 4
      grect x y 3.5 3.5
      x += 1.75
      y += 1.75
      for d to 4 : if e[4 * i + d] <> -1
         gcircle x + dx[d] * 2, y + dy[d] * 2, 1.2
      .
   .
   gcolor 057
   x = 4 * x[pos] + 2 + offx * 4
   y = 4 * y[pos] + 2 + offy * 4
   gcircle x y 1.2
   gcolor 000
   gcircle x + dx[dir] * 0.7 y + dy[dir] * 0.7 0.3
.
gbackground 000
# 
proc init .
   x[] = [ 12 ]
   y[] = [ 12 ]
   dir = 1
   # alignment padding
   e[] = [ 0 0 0 0 ]
   for n = 1 to 4 : e[] &= random 2 - 2
   offx = 0
   offy = 0
   pos = 1
   draw
   gcolor 777
   gtextsize 5
   gtext 8 85 "arrow keys to navigate"
   gtext 8 78 "n for new maze"
.
init
# 
on key_down
   k$ = keybkey
   # for absolute navigation
   # dir = 2   
   if k$ = "ArrowUp"
      h = dir mod1 4
   elif k$ = "ArrowLeft"
      h = (dir + 1) mod1 4
   elif k$ = "ArrowDown"
      h = (dir + 2) mod1 4
   elif k$ = "ArrowRight"
      h = (dir + 3) mod1 4
   else
      if k$ = "n" : init
      return
   .
   if e[4 * pos + h] = -1 : return
   npos = e[4 * pos + h]
   dir = h
   if npos = 0
      x[] &= x[pos] + dx[dir]
      y[] &= y[pos] + dy[dir]
      pos = len x[]
      for d = 1 to 4 : e[] &= random 2 - 2
      for i to len x[] : for d = 1 to 4
         if x[pos] + dx[d] = x[i] and y[pos] + dy[d] = y[i]
            e[4 * pos + d] = e[4 * i + (d + 2) mod1 4]
            if e[4 * pos + d] = 0
               e[4 * pos + d] = i
               e[4 * i + (d + 2) mod1 4] = pos
            .
         .
      .
   else
      pos = npos
   .
   draw
.
