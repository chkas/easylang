func[] digcnt n .
   len cnt[] 10
   while n > 0
      cnt[n mod 10 + 1] += 1
      n = n div 10
   .
   return cnt[]
.
groups[][] = [ ]
groupsi[] = [ ]
#
proc show_first kind$ count base places .
   print "\nFirst shuffle group with " & kind$ & " witnesses"
   print " " & count & " [" & base & " " & strjoin groups[count][] " " & "]"
   print "\nFor the first " & count & " shuffle groups, there are:"
   len cw[] 9
   for wits[] in groups[][] : cw[len wits[]] += 1
   for k = 9 downto 1 : if cw[k] > 0
      numfmt places 0
      write cw[k]
      numfmt 0 2
      print "  with " & k & " witness(es)"
   .
.
global first20 wits4p wits3 .
proc test i p .
   for j = 2 to 9
      m = i * j
      if m >= p : break 1
      if len digs[] = 0 : digs[] = digcnt i
      digs2[] = digcnt m
      if digs[] = digs2[] : wits[] &= j
   .
   if len wits[] > 0
      groups[][] &= wits[]
      groupsi[] &= i
      c = len groupsi[]
      if first20 = 0 and c = 20
         print "First 20 shuffle groups:"
         for k = 1 to 20
            gr$ = strjoin groups[k][] " "
            print " " & k & " [" & groupsi[k] & " " & gr$ & "]"
         .
         first20 = 1
      .
      if wits4p = 0 and len wits[] > 4
         show_first "more than 4" c i 3
         wits4p = 1
      .
      if wits3 = 0 and len wits[] = 3
         show_first "exactly 3" c i 3
         wits3 = 1
      .
   .
.
i = 12
p = 100
hp = 50
repeat
   test i p
   until wits3 = 1
   i += 1
   if i mod 10 = 0
      if i = hp
         i = p
         p *= 10
         hp *= 10
      .
      i += 1
   .
.
